
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//получим используемые комплектующие
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(РесурсныеСпецификацииМатериалыИУслуги.Номенклатура) КАК НоменклатураПредставление,
	|	РесурсныеСпецификацииМатериалыИУслуги.Количество КАК Количество,
	|	РесурсныеСпецификацииМатериалыИУслуги.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрСведений.ОсновныеСпецификации КАК ОсновныеСпецификации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК РесурсныеСпецификацииМатериалыИУслуги
	|		ПО ОсновныеСпецификации.Спецификация = РесурсныеСпецификацииМатериалыИУслуги.Ссылка
	|ГДЕ
	|	ОсновныеСпецификации.Номенклатура = &Номенклатура
	|	И РесурсныеСпецификацииМатериалыИУслуги.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|	И НЕ РесурсныеСпецификацииМатериалыИУслуги.Номенклатура.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Номенклатура", Параметры.Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		
		СтрокаНоменклатура = ГИГ_Обеспечение.ПолучитьЭлементы().Добавить();
		СтрокаНоменклатура.Номенклатура = ВыборкаДетальныеЗаписи.НоменклатураПредставление;
		ЭлементыНоменклатуры = СтрокаНоменклатура.ПолучитьЭлементы();
		
		ЗапросОстатков = Новый Запрос;
		ЗапросОстатков.Текст = 
		"ВЫБРАТЬ
		|	""ТоварыНаСкладах"" КАК Источник,
		|	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК Остаток,
		|	1 КАК СрокПоставки
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(
		|			,
		|			Склад = &СкладСвой
		|				И Номенклатура = &Номенклатура
		|				И Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК ТоварыНаСкладахОстатки
		|ГДЕ
		|	ТоварыНаСкладахОстатки.ВНаличииОстаток >= &ТребуемоеКоличество
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""ТоварыПланируемоеПоступление"",
		|	ДвижениеТоваровОбороты.ПланируемоеПоступлениеОборот,
		|	РАЗНОСТЬДАТ(&ТекущаяДата, ДвижениеТоваровОбороты.Распоряжение.ДатаПоступления, ДЕНЬ)
		|ИЗ
		|	РегистрНакопления.ДвижениеТоваров.Обороты(
		|			,
		|			,
		|			,
		|			Склад = &СкладСвой
		|				И Номенклатура = &Номенклатура
		|				И Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК ДвижениеТоваровОбороты
		|ГДЕ
		|	ДвижениеТоваровОбороты.ПланируемоеПоступлениеОборот >= &ТребуемоеКоличество
		|	И ДвижениеТоваровОбороты.Распоряжение ССЫЛКА Документ.ЗаказПоставщику
		|	И РАЗНОСТЬДАТ(&ТекущаяДата, ДвижениеТоваровОбороты.Распоряжение.ДатаПоступления, ДЕНЬ) > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""ТоварыСкладПоставщиков"",
		|	ГИГ_НоменклатураНаСкладеПоставщика.Количество,
		|	ГИГ_СрокиПоставкиНаСкладПоставщикаСПроизводства.СрокПоставки
		|ИЗ
		|	РегистрСведений.ГИГ_НоменклатураНаСкладеПоставщика КАК ГИГ_НоменклатураНаСкладеПоставщика
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ГИГ_СрокиПоставкиНаСкладПоставщикаСПроизводства КАК ГИГ_СрокиПоставкиНаСкладПоставщикаСПроизводства
		|		ПО ГИГ_НоменклатураНаСкладеПоставщика.Номенклатура = ГИГ_СрокиПоставкиНаСкладПоставщикаСПроизводства.Номенклатура
		|			И ГИГ_НоменклатураНаСкладеПоставщика.Склад = ГИГ_СрокиПоставкиНаСкладПоставщикаСПроизводства.СкладПоставщика
		|ГДЕ
		|	ГИГ_НоменклатураНаСкладеПоставщика.Номенклатура = &Номенклатура
		|	И ГИГ_СрокиПоставкиНаСкладПоставщикаСПроизводства.Номенклатура = &Номенклатура
		|	И ГИГ_НоменклатураНаСкладеПоставщика.Количество >= &ТребуемоеКоличество
		|	И ГИГ_НоменклатураНаСкладеПоставщика.Склад = &СкладПоставщика
		|	И ГИГ_СрокиПоставкиНаСкладПоставщикаСПроизводства.СкладПоставщика = &СкладПоставщика
		|	И ГИГ_СрокиПоставкиНаСкладПоставщикаСПроизводства.СрокПоставки > 0
		|	И ГИГ_НоменклатураНаСкладеПоставщика.Количество > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	СрокПоставки";
		
		ЗапросОстатков.УстановитьПараметр("Номенклатура", ВыборкаДетальныеЗаписи.Номенклатура);
		ЗапросОстатков.УстановитьПараметр("СкладПоставщика", Параметры.СкладПоставщика);
		ЗапросОстатков.УстановитьПараметр("СкладСвой", Параметры.Склад); 
		ЗапросОстатков.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
		ЗапросОстатков.УстановитьПараметр("ТребуемоеКоличество", Параметры.ТребуемоеКоличество * ВыборкаДетальныеЗаписи.Количество); //в спецификации на производство одного изделия может ребоваться разное количество
		
		РезультатЗапросаОстатков = ЗапросОстатков.Выполнить();
		
		ВыборкаНоменклатура = РезультатЗапросаОстатков.Выбрать();
		
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			СтрокаДействия = ЭлементыНоменклатуры.Добавить();
			
			Если ВыборкаНоменклатура.Источник = "ТоварыНаСкладах" Тогда	
				СтрокаДействия.Номенклатура = "Резервировать на складе";
			ИначеЕсли  ВыборкаНоменклатура.Источник = "ТоварыПланируемоеПоступление" Тогда
				СтрокаДействия.Номенклатура = "Резервировать к дате";
			ИначеЕсли  ВыборкаНоменклатура.Источник = "ТоварыСкладПоставщиков" Тогда	
				СтрокаДействия.Номенклатура = "К обеспечению";
			КонецЕсли;
			
			СтрокаДействия.СрокПоставки  = ВыборкаНоменклатура.СрокПоставки;
			СтрокаДействия.ДатаОбеспечения  = ТекущаяДата() + ВыборкаНоменклатура.СрокПоставки*24*60*60;
			
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры
