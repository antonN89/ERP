// + [Rineco], [Киселев А.] [14.12.2021] [Log_Diff][№ 21317], [#ТиповойНаборМатериалов] 
&НаКлиенте
Процедура ОтметитьВсе(Команда)
	УстановитьСнятьОтметки(Истина);
КонецПроцедуры

// + [Rineco], [Киселев А.] [14.12.2021] [Log_Diff][№ 21317], [#ТиповойНаборМатериалов] 
&НаКлиенте
Процедура УстановитьСнятьОтметки(ЗначениеОтметки)
	
	Для Каждого ЭлементСписка Из СписокТиповыхНаборов Цикл 
		ЭлементСписка.Пометка = ЗначениеОтметки;
	КонецЦикла;
	
КонецПроцедуры

// + [Rineco], [Киселев А.] [14.12.2021] [Log_Diff][№ 21317], [#ТиповойНаборМатериалов] 
&НаКлиенте
Процедура УбратьОтметки(Команда)
	УстановитьСнятьОтметки(Ложь);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Рин1_ТиповойНаборМатериалов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Рин1_ТиповойНаборМатериалов КАК Рин1_ТиповойНаборМатериалов
		|ГДЕ
		|	НЕ Рин1_ТиповойНаборМатериалов.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СписокТиповыхНаборов.Добавить(ВыборкаДетальныеЗаписи.Ссылка,,Истина);
	КонецЦикла;
	
КонецПроцедуры

// + [Rineco], [Киселев А.] [14.12.2021] [Log_Diff][№ 21317], [#ТиповойНаборМатериалов] 
&НаКлиенте
Процедура ОК(Команда)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	СформироватьПечатнуюФормуСервер(ТабличныйДокумент);
	
	ТабличныйДокумент.Показать();
	
КонецПроцедуры

// + [Rineco], [Киселев А.] [14.12.2021] [Log_Diff][№ 21317], [#ТиповойНаборМатериалов] 
&НаСервере
Процедура СформироватьПечатнуюФормуСервер(ТабличныйДокумент)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Макет = ОбработкаОбъект.ПолучитьМакет("СписокТиповыхМатериалов");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ТабличныйДокумент.Вывести(ОбластьШапка);
	ОбластьДетальныеЗаписиТиповойНабор = Макет.ПолучитьОбласть("ДетальныеЗаписиТиповойНаборМатериалов");
	ОбластьДетальныеЗаписи = Макет.ПолучитьОбласть("ДетальныеЗаписиНоменклатура");
	
	МассивНаборов = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(СписокТиповыхНаборов);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Рин1_СопоставлениеТиповыхНаборовМатериалов.ТиповойНаборМатериалов КАК ТиповойНаборМатериалов,
		|	Рин1_СопоставлениеТиповыхНаборовМатериалов.Номенклатура КАК Номенклатура,
		|	Рин1_СопоставлениеТиповыхНаборовМатериалов.Номенклатура.Артикул КАК НоменклатураАртикул,
		|	Рин1_СопоставлениеТиповыхНаборовМатериалов.Порядок КАК Порядок
		|ИЗ
		|	РегистрСведений.Рин1_СопоставлениеТиповыхНаборовМатериалов КАК Рин1_СопоставлениеТиповыхНаборовМатериалов
		|ГДЕ
		|	Рин1_СопоставлениеТиповыхНаборовМатериалов.ТиповойНаборМатериалов В(&МассивНаборов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Рин1_СопоставлениеТиповыхНаборовМатериалов.Порядок
		|ИТОГИ ПО
		|	ТиповойНаборМатериалов";
	
	Запрос.УстановитьПараметр("МассивНаборов", МассивНаборов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаТиповойНаборМатериалов = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаТиповойНаборМатериалов.Следующий() Цикл
		
		ОбластьДетальныеЗаписиТиповойНабор.Параметры.ТиповойНаборМатериалов = ВыборкаТиповойНаборМатериалов.ТиповойНаборМатериалов;
		
		ТабличныйДокумент.Вывести(ОбластьДетальныеЗаписиТиповойНабор);
	
		ВыборкаДетальныеЗаписи = ВыборкаТиповойНаборМатериалов.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Если ВыборкаДетальныеЗаписи.Порядок % 2 = 0 Тогда
				ОбластьДетальныеЗаписи.Параметры.АртикулПраво = ВыборкаДетальныеЗаписи.НоменклатураАртикул;
				ОбластьДетальныеЗаписи.Параметры.НоменклатураПраво = ВыборкаДетальныеЗаписи.Номенклатура;	
			Иначе 
				ОбластьДетальныеЗаписи.Параметры.АртикулЛево = ВыборкаДетальныеЗаписи.НоменклатураАртикул;
				ОбластьДетальныеЗаписи.Параметры.НоменклатураЛево = ВыборкаДетальныеЗаписи.Номенклатура;	
			КонецЕсли;
			
			
			ТабличныйДокумент.Вывести(ОбластьДетальныеЗаписи);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры