
&Вместо("ОбработкаПроверкиЗаполнения")
Процедура Рин1_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Перем Ошибки;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	//bercut200320 задача 132п1
	Если не ЗначениеЗаполнено(Дата) Тогда
		
		ТекстОшибки = НСтр("ru = 'Дата договора должна быть заполнена';
		|en = 'Contract cannot be less than the contract date'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		ЭтотОбъект, 
		"Дата",
		,
		Отказ);
		
	КонецЕсли;
	
	Если не ЗначениеЗаполнено(ДатаНачалаДействия) Тогда
		
		ТекстОшибки = НСтр("ru = 'Период действия договора должен быть заполнен';
		|en = 'Contract cannot be less than the contract date'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		ЭтотОбъект, 
		"ДатаНачалаДействия",
		,
		Отказ);
		
	КонецЕсли;
	
	Если не ЗначениеЗаполнено(ДатаОкончанияДействия) Тогда
		
		ТекстОшибки = НСтр("ru = 'Период действия договора должен быть заполнен';
		|en = 'Contract cannot be less than the contract date'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		ЭтотОбъект, 
		"ДатаОкончанияДействия",
		,
		Отказ);
		
	КонецЕсли;
	
	Если не ЗначениеЗаполнено(Менеджер) Тогда
		
		ТекстОшибки = НСтр("ru = 'Менеджер  должен быть заполнен';
		|en = 'Contract cannot be less than the contract date'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		ЭтотОбъект, 
		"Менеджер",
		,
		Отказ);
		
	КонецЕсли;
	
	Если не ЗначениеЗаполнено(ГрафикОплаты) Тогда
		
		ТекстОшибки = НСтр("ru = 'График оплаты  должен быть заполнен';
		|en = 'Contract cannot be less than the contract date'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		ЭтотОбъект, 
		"ГрафикОплаты",
		,
		Отказ);
		
	КонецЕсли;
	//
	
	// Дата начала действия договора должна быть не меньше, чем дата договора.
	Если ЗначениеЗаполнено(Дата) И ЗначениеЗаполнено(ДатаНачалаДействия) Тогда
		
		Если НачалоДня(Дата) > ДатаНачалаДействия Тогда
			
			ТекстОшибки = НСтр("ru = 'Дата начала действия договора должна быть не меньше даты договора';
			|en = 'Contract effective date cannot be less than the contract date'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект, 
			"ДатаНачалаДействия",
			,
			Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Дата окончания действия договора должна быть не меньше, чем дата договора.
	Если ЗначениеЗаполнено(Дата) И ЗначениеЗаполнено(ДатаОкончанияДействия) Тогда	
		
		Если НачалоДня(Дата) > ДатаОкончанияДействия Тогда
			
			ТекстОшибки = НСтр("ru = 'Дата окончания действия договора должна быть не меньше даты договора';
			|en = 'Contract expiration date cannot be less than the contract date'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект, 
			"ДатаОкончанияДействия",
			,
			Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Дата окончания действия договора должна быть не меньше, чем дата начала действия.
	Если ЗначениеЗаполнено(ДатаНачалаДействия) И ЗначениеЗаполнено(ДатаОкончанияДействия) Тогда	
		
		Если ДатаНачалаДействия > ДатаОкончанияДействия Тогда
			
			ТекстОшибки = НСтр("ru = 'Дата окончания действия договора должна быть не меньше даты начала действия';
			|en = 'Contract expiration date cannot be less than contract effective date'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект, 
			"ДатаОкончанияДействия",
			,
			Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Партнер");
	
	Если НЕ ЗначениеЗаполнено(Партнер) Тогда
		
		ТекстОшибки = НСтр("ru = 'Поле ""%1"" не заполнено';
		|en = 'The ""%1"" field is not filled in'");
		ЗаголовокПартнер = ПартнерыИКонтрагенты.ЗаголовокРеквизитаПартнерВЗависимостиОтХозяйственнойОперации(ХозяйственнаяОперация);
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ЗаголовокПартнер);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		ЭтотОбъект, 
		"Партнер",
		,
		Отказ);
		
	КонецЕсли;
	
	Если Не УчетАгентскогоНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВидАгентскогоДоговора");
	КонецЕсли;
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если ЗначениеЗаполнено(НаправлениеДеятельности)
		ИЛИ НЕ НаправленияДеятельностиСервер.УказаниеНаправленияДеятельностиОбязательно(ХозяйственнаяОперация) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НаправлениеДеятельности");
	КонецЕсли;
	
	//++ НЕ УТ
	Если Не ДоговорСУчастникомГОЗ Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГосударственныйКонтракт");
	КонецЕсли;
	
	Если Не ПлатежиПо275ФЗ Или (ТипДоговора <> Перечисления.ТипыДоговоров.СПоставщиком) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ТипПлатежаФЗ275");
	КонецЕсли;
	
	Если ПлатежиПо275ФЗ И ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком Тогда
		
		ДенежныеСредстваСервер.ОбработкаПроверкиЗаполненияПодтверждающиеДокументы(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, Ошибки);
		ДенежныеСредстваСервер.ПроверитьЗаполнениеРеквизитовПлатежаГОЗ(ЭтотОбъект, Отказ);
		
		Если ДоговорСУчастникомГОЗ Тогда
			Если ЗначениеЗаполнено(БанковскийСчет) И ЗначениеЗаполнено(ГосударственныйКонтракт)
				И ГосударственныйКонтракт <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "ГосударственныйКонтракт") Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Государственный контракт, к которому привязан банковский счет, не должен отличаться от контракта, выбранного в договоре.';
				|en = 'State contract to which the bank account is linked cannot differ from the contract selected in the contract.'"),
				ЭтотОбъект,
				"БанковскийСчет",,
				Отказ);
			КонецЕсли;
			Если ЗначениеЗаполнено(БанковскийСчетКонтрагента) И ЗначениеЗаполнено(ГосударственныйКонтракт)
				И ГосударственныйКонтракт <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчетКонтрагента, "ГосударственныйКонтракт") Тогда
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Государственный контракт, к которому привязан банковский счет, не должен отличаться от контракта, выбранного в договоре.';
				|en = 'State contract to which the bank account is linked cannot differ from the contract selected in the contract.'"),
				ЭтотОбъект,
				"БанковскийСчетКонтрагента",,
				Отказ);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ПлатежиПо275ФЗ И ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем Тогда
		
		Если ЗначениеЗаполнено(БанковскийСчет) И ЗначениеЗаполнено(ГосударственныйКонтракт)
			И ГосударственныйКонтракт <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "ГосударственныйКонтракт") Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Государственный контракт, к которому привязан банковский счет, не должен отличаться от контракта, выбранного в договоре.';
			|en = 'State contract to which the bank account is linked cannot differ from the contract selected in the contract.'"),
			ЭтотОбъект,
			"БанковскийСчет",,
			Отказ);
		КонецЕсли;
		Если ЗначениеЗаполнено(БанковскийСчетКонтрагента) И ЗначениеЗаполнено(ГосударственныйКонтракт)
			И ГосударственныйКонтракт <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчетКонтрагента, "ГосударственныйКонтракт") Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Государственный контракт, к которому привязан банковский счет, не должен отличаться от контракта, выбранного в договоре.';
			|en = 'State contract to which the bank account is linked cannot differ from the contract selected in the contract.'"),
			ЭтотОбъект,
			"БанковскийСчетКонтрагента",,
			Отказ);
		КонецЕсли;
	КонецЕсли;
	//-- НЕ УТ
	
	ИспользоватьОформлениеЗакупок = (ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком
	Или ТипДоговора = Перечисления.ТипыДоговоров.ВвозИзЕАЭС
	Или ТипДоговора = Перечисления.ТипыДоговоров.Импорт);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой")
		Или СпособДоставки = Перечисления.СпособыДоставки.ОпределяетсяВРаспоряжении
		Или Не ИспользоватьОформлениеЗакупок Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СпособДоставки");
		МассивНепроверяемыхРеквизитов.Добавить("ПеревозчикПартнер");
		МассивНепроверяемыхРеквизитов.Добавить("АдресДоставкиПеревозчика");
	КонецЕсли;
	
	ДоставкаТоваров.ПроверитьЗаполнениеРеквизитовДоставки(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	
	Если НалогообложениеНДСОпределяетсяВДокументе 
		Или ТипДоговора = Перечисления.ТипыДоговоров.Импорт
		Или ТипДоговора = Перечисления.ТипыДоговоров.СПереработчиком Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НалогообложениеНДС");
	КонецЕсли;
	
	Если ЗакупкаПодДеятельностьОпределяетсяВДокументе
		Или (ТипДоговора <> Перечисления.ТипыДоговоров.СПоставщиком
		И ТипДоговора <> Перечисления.ТипыДоговоров.СПереработчиком
		И ТипДоговора <> Перечисления.ТипыДоговоров.СПоклажедателем) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗакупкаПодДеятельность");
	КонецЕсли;
	
	Если ТипДоговора <> Перечисления.ТипыДоговоров.СПоклажедателем Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПорядокОформленияСписанияНедостачПринятыхНаХранениеТоваров");
	КонецЕсли;	
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

// + [Rineco], [Киселев А.] [27.07.2021] 
// Задача: [№ 12111], [# Установить флаг по умолчанию]
&После("ПриКопировании")
Процедура Рин1_ПриКопировании(ОбъектКопирования)
	
	Если Не ЗапрещаетсяПросроченнаяЗадолженность Тогда  
		ЗапрещаетсяПросроченнаяЗадолженность = Истина;	
	КонецЕсли;
	
КонецПроцедуры
// - [Rineco], [Киселев А.] [27.07.2021

// + [Rineco], [Киселев А.Н.] [19.10.2021] 
// Задача: [№ 20449], [#ДобавитьСтатусВИсторию]
&После("ПередЗаписью")
Процедура Рин1_ПередЗаписью(Отказ)
	
	Если Не Отказ Тогда
		
		Если Не ЭтоНовый() И ЭтотОбъект.РИНЭКО_СтатусДЗ <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.Ссылка,"РИНЭКО_СтатусДЗ") Тогда
			РегистрыСведений.РИНЭКО_ИсторияИзмененияСтатуса.ДобавитьНовуюЗапись(ЭтотОбъект.Ссылка,ЭтотОбъект.РИНЭКО_СтатусДЗ);		
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// + [Rineco], [Киселев А.Н.] [19.10.2021] 
// Задача: [№ 20449], [#ДобавитьСтатусВИсторию]
&После("ПриЗаписи")
Процедура Рин1_ПриЗаписи(Отказ)
	
	Если Не Отказ И ЭтоНовый() Тогда
		РегистрыСведений.РИНЭКО_ИсторияИзмененияСтатуса.ДобавитьНовуюЗапись(ЭтотОбъект.Ссылка,ЭтотОбъект.РИНЭКО_СтатусДЗ);		
	КонецЕсли;
	
КонецПроцедуры

