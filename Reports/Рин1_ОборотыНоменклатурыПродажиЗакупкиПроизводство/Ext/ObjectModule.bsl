// + [Rineco], [Киселев А.] [22.07.2021] 
// Задача: [№ 12835 ], [# Выполняем запрос и передаем в СКД (Привелигированный режим)]
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Схема = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Настройки, ДанныеРасшифровки,ПолучитьМакет("МакетОформления"));
	
	ТаблицаДанных = СформироватьТаблицуДанных(ДатаНачала,ДатаОкончания,Номенклатура);
	ВнешнийНаборДанных = Новый Структура("ТаблицаДанных", ТаблицаДанных); 
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНаборДанных, ДанныеРасшифровки); 
	
	ДокументРезультат.Очистить();
	
 	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент; 
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат); 
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// - [Rineco], [Киселев А.] [22.07.2021]

// + [Rineco], [Киселев А.] [22.07.2021] 
// Задача: [№ 12835 ], [# Получаем таблицу для заполнения СКД]
Функция СформироватьТаблицуДанных(ДатаНачала,ДатаОкончания,Номенклатура)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказКлиентаТовары.Ссылка КАК Заказ,
	|	ЗаказКлиентаТовары.Ссылка.Статус КАК Статус,
	|	СУММА(ВЫБОР
	|			КОГДА ЗаказКлиентаТовары.Отменено
	|				ТОГДА 0
	|			ИНАЧЕ ЗаказКлиентаТовары.Количество
	|		КОНЕЦ) КАК Количество,
	|	ЗаказКлиентаТовары.Отменено КАК Отменено,
	|	ЗаказКлиентаТовары.Склад КАК Склад,
	|	ЗаказКлиентаТовары.Ссылка.Контрагент КАК Контрагент,
	|	ЗаказКлиентаТовары.Ссылка.Организация КАК Организация,
	|	ЗаказКлиентаТовары.Ссылка.Менеджер КАК Менеджер,
	|	ЗаказКлиентаТовары.Номенклатура.ЕдиницаИзмерения КАК Упаковка
	|ПОМЕСТИТЬ ВТ_ЗаказыКлиента
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|ГДЕ
	|	ЗаказКлиентаТовары.Номенклатура = &Номенклатура
	|	И ЗаказКлиентаТовары.Ссылка.Проведен
	|	И ЗаказКлиентаТовары.Ссылка.Дата >= &ДатаНачала
	|	И ЗаказКлиентаТовары.Ссылка.Дата <= &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказКлиентаТовары.Ссылка,
	|	ЗаказКлиентаТовары.Ссылка.Статус,
	|	ЗаказКлиентаТовары.Отменено,
	|	ЗаказКлиентаТовары.Склад,
	|	ЗаказКлиентаТовары.Ссылка.Контрагент,
	|	ЗаказКлиентаТовары.Ссылка.Организация,
	|	ЗаказКлиентаТовары.Ссылка.Менеджер,
	|	ЗаказКлиентаТовары.Номенклатура.ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыручкаИСебестоимостьПродажОбороты.Регистратор КАК Регистратор,
	|	ВыручкаИСебестоимостьПродажОбороты.ЗаказКлиента КАК ЗаказКлиента,
	|	ВыручкаИСебестоимостьПродажОбороты.Партия КАК Партия,
	|	СУММА(ВыручкаИСебестоимостьПродажОбороты.КоличествоОборот) КАК КоличествоРТУ,
	|	ВыручкаИСебестоимостьПродажОбороты.Склад КАК Склад,
	|	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	ВыручкаИСебестоимостьПродажОбороты.Регистратор.Ответственный КАК Менеджер
	|ПОМЕСТИТЬ ВТ_Реализации
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&ДатаНачала, &ТекущаяДата, Регистратор, АналитикаУчетаНоменклатуры.Номенклатура = &Номенклатура) КАК ВыручкаИСебестоимостьПродажОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыручкаИСебестоимостьПродажОбороты.Регистратор,
	|	ВыручкаИСебестоимостьПродажОбороты.ЗаказКлиента,
	|	ВыручкаИСебестоимостьПродажОбороты.Партия,
	|	ВыручкаИСебестоимостьПродажОбороты.Склад,
	|	ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаПоПартнерам.Контрагент,
	|	ВыручкаИСебестоимостьПродажОбороты.Регистратор.Ответственный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ЗаказыКлиента.Заказ КАК Заказ,
	|	ВТ_ЗаказыКлиента.Статус КАК Статус,
	|	ВТ_ЗаказыКлиента.Количество КАК КоличествоЗаказ,
	|	ВТ_ЗаказыКлиента.Отменено КАК Отменено,
	|	ВТ_ЗаказыКлиента.Склад КАК Склад,
	|	ВТ_ЗаказыКлиента.Контрагент КАК Контрагент,
	|	ВТ_ЗаказыКлиента.Организация КАК Организация,
	|	ВТ_Реализации.Регистратор КАК Реализация,
	|	ВТ_Реализации.КоличествоРТУ КАК КоличествоРТУ,
	|	ВТ_Реализации.Партия КАК Партия,
	|	ВТ_ЗаказыКлиента.Менеджер КАК Менеджер,
	|	ВТ_ЗаказыКлиента.Упаковка КАК Упаковка,
	|	СостоянияЗаказовКлиентов.Состояние КАК Состояние
	|ПОМЕСТИТЬ Продажи
	|ИЗ
	|	ВТ_ЗаказыКлиента КАК ВТ_ЗаказыКлиента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Реализации КАК ВТ_Реализации
	|		ПО ВТ_ЗаказыКлиента.Заказ = ВТ_Реализации.ЗаказКлиента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЗаказовКлиентов КАК СостоянияЗаказовКлиентов
	|		ПО ВТ_ЗаказыКлиента.Заказ = СостоянияЗаказовКлиентов.Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПоставщикуТовары.Ссылка КАК Заказ,
	|	ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ЗаказПоставщикуТовары.Отменено
	|				ТОГДА ЗаказПоставщикуТовары.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоЗаказ,
	|	ЗаказПоставщикуТовары.Отменено КАК Отменено,
	|	ЗаказПоставщикуТовары.Ссылка.Статус КАК Статус,
	|	ЗаказПоставщикуТовары.Склад КАК Склад,
	|	ЗаказПоставщикуТовары.Ссылка.Контрагент КАК Контрагент,
	|	ЗаказПоставщикуТовары.Ссылка.Менеджер КАК Менеджер,
	|	ЗаказПоставщикуТовары.Ссылка.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ЗаказыПоставщику
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|ГДЕ
	|	ЗаказПоставщикуТовары.Ссылка.Проведен
	|	И ЗаказПоставщикуТовары.Номенклатура = &Номенклатура
	|	И ЗаказПоставщикуТовары.Ссылка.Дата >= &ДатаНачала
	|	И ЗаказПоставщикуТовары.Ссылка.Дата <= &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПоставщикуТовары.Ссылка,
	|	ЗаказПоставщикуТовары.Отменено,
	|	ЗаказПоставщикуТовары.Ссылка.Статус,
	|	ЗаказПоставщикуТовары.Склад,
	|	ЗаказПоставщикуТовары.Ссылка.Контрагент,
	|	ЗаказПоставщикуТовары.Ссылка.Менеджер,
	|	ЗаказПоставщикуТовары.Ссылка.Организация,
	|	ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыПоставщикамОбороты.Регистратор КАК Регистратор,
	|	ЗаказыПоставщикамОбороты.ЗаказПоставщику КАК ЗаказПоставщику,
	|	СУММА(ЗаказыПоставщикамОбороты.ЗаказаноРасход) КАК КоличествоПТУ,
	|	ЗаказыПоставщикамОбороты.Склад КАК Склад,
	|	ЗаказыПоставщикамОбороты.Регистратор.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ ВТ_Поступления
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам.Обороты(&ДатаНачала, &ТекущаяДата, Регистратор, Номенклатура = &Номенклатура) КАК ЗаказыПоставщикамОбороты
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ЗаказыПоставщикамОбороты.Регистратор) = ТИП(Документ.ПриобретениеТоваровУслуг)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыПоставщикамОбороты.Регистратор,
	|	ЗаказыПоставщикамОбороты.ЗаказПоставщику,
	|	ЗаказыПоставщикамОбороты.Склад,
	|	ЗаказыПоставщикамОбороты.Регистратор.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ЗаказыПоставщику.Заказ КАК Заказ,
	|	ВТ_ЗаказыПоставщику.Упаковка КАК Упаковка,
	|	ВТ_ЗаказыПоставщику.КоличествоЗаказ КАК КоличествоЗаказ,
	|	ВТ_ЗаказыПоставщику.Отменено КАК Отменено,
	|	ВТ_ЗаказыПоставщику.Статус КАК Статус,
	|	ВТ_ЗаказыПоставщику.Склад КАК Склад,
	|	ВТ_ЗаказыПоставщику.Контрагент КАК Контрагент,
	|	ВТ_ЗаказыПоставщику.Менеджер КАК Менеджер,
	|	ВТ_Поступления.Регистратор КАК Регистратор,
	|	ВТ_Поступления.КоличествоПТУ КАК КоличествоПТУ,
	|	ВТ_ЗаказыПоставщику.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка) КАК Партия,
	|	СостоянияЗаказовПоставщикам.Состояние КАК Состояние
	|ПОМЕСТИТЬ Закупки
	|ИЗ
	|	ВТ_ЗаказыПоставщику КАК ВТ_ЗаказыПоставщику
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Поступления КАК ВТ_Поступления
	|		ПО ВТ_ЗаказыПоставщику.Заказ = ВТ_Поступления.ЗаказПоставщику
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЗаказовПоставщикам КАК СостоянияЗаказовПоставщикам
	|		ПО ВТ_ЗаказыПоставщику.Заказ = СостоянияЗаказовПоставщикам.Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбеспечениеПроизводственныхПроцессов.Распоряжение КАК Заказ,
	|	ВЫБОР
	|		КОГДА ОбеспечениеПроизводственныхПроцессов.Регистратор = ОбеспечениеПроизводственныхПроцессов.Распоряжение
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяССылка)
	|		ИНАЧЕ ОбеспечениеПроизводственныхПроцессов.Регистратор
	|	КОНЕЦ КАК Отгрузка,
	|	ОбеспечениеПроизводственныхПроцессов.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ОбеспечениеПроизводственныхПроцессов.Регистратор = ОбеспечениеПроизводственныхПроцессов.Распоряжение
	|			ТОГДА ОбеспечениеПроизводственныхПроцессов.Требуется
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоЗаказ,
	|	ВЫБОР
	|		КОГДА ОбеспечениеПроизводственныхПроцессов.Регистратор <> ОбеспечениеПроизводственныхПроцессов.Распоряжение
	|				И ОбеспечениеПроизводственныхПроцессов.Запланировано > 0
	|			ТОГДА ОбеспечениеПроизводственныхПроцессов.Запланировано
	|		КОГДА ОбеспечениеПроизводственныхПроцессов.Регистратор <> ОбеспечениеПроизводственныхПроцессов.Распоряжение
	|				И ОбеспечениеПроизводственныхПроцессов.Произведено > 0
	|			ТОГДА ОбеспечениеПроизводственныхПроцессов.Произведено
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоРТУ,
	|	ОбеспечениеПроизводственныхПроцессов.Распоряжение.Статус КАК Статус,
	|	ОбеспечениеПроизводственныхПроцессов.Распоряжение.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяССылка) КАК Контрагент,
	|	ОбеспечениеПроизводственныхПроцессов.Распоряжение.Ответственный КАК Менеджер,
	|	ОбеспечениеПроизводственныхПроцессов.Номенклатура.ЕдиницаИзмерения КАК Упаковка
	|ПОМЕСТИТЬ ВТ_Заказ_Этап
	|ИЗ
	|	РегистрНакопления.ОбеспечениеПроизводственныхПроцессов КАК ОбеспечениеПроизводственныхПроцессов
	|ГДЕ
	|	ОбеспечениеПроизводственныхПроцессов.Номенклатура = &Номенклатура
	|	И ОбеспечениеПроизводственныхПроцессов.Период >= &ДатаНачала
	|	И ОбеспечениеПроизводственныхПроцессов.Период <= &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Заказ_Этап.Заказ КАК Заказ,
	|	ВТ_Заказ_Этап.Отгрузка КАК Отгрузка,
	|	ВТ_Заказ_Этап.Склад КАК Склад,
	|	СУММА(ВТ_Заказ_Этап.КоличествоЗаказ) КАК КоличествоЗаказ,
	|	СУММА(ВТ_Заказ_Этап.КоличествоРТУ) КАК КоличествоРТУ,
	|	ВТ_Заказ_Этап.Статус КАК Статус,
	|	ВТ_Заказ_Этап.Организация КАК Организация,
	|	ВТ_Заказ_Этап.Контрагент КАК Контрагент,
	|	ВТ_Заказ_Этап.Менеджер КАК Менеджер,
	|	ВТ_Заказ_Этап.Упаковка КАК Упаковка,
	|	МАКСИМУМ(СостоянияЗаказовНаПроизводство.СостояниеВыпуска) КАК Состояние
	|ПОМЕСТИТЬ ВТ_ЗаказыНаПроизводство
	|ИЗ
	|	ВТ_Заказ_Этап КАК ВТ_Заказ_Этап
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЗаказовНаПроизводство КАК СостоянияЗаказовНаПроизводство
	|		ПО (СостоянияЗаказовНаПроизводство.Заказ = ВТ_Заказ_Этап.Заказ)
	|ГДЕ
	|	ВТ_Заказ_Этап.Отгрузка = ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Заказ_Этап.Заказ,
	|	ВТ_Заказ_Этап.Отгрузка,
	|	ВТ_Заказ_Этап.Склад,
	|	ВТ_Заказ_Этап.Статус,
	|	ВТ_Заказ_Этап.Организация,
	|	ВТ_Заказ_Этап.Контрагент,
	|	ВТ_Заказ_Этап.Менеджер,
	|	ВТ_Заказ_Этап.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Заказ_Этап.Заказ КАК Заказ,
	|	ВТ_Заказ_Этап.Отгрузка КАК Отгрузка,
	|	ВТ_Заказ_Этап.Склад КАК Склад,
	|	СУММА(ВТ_Заказ_Этап.КоличествоЗаказ) КАК КоличествоЗаказ,
	|	СУММА(ВТ_Заказ_Этап.КоличествоРТУ) КАК КоличествоРТУ,
	|	ВТ_Заказ_Этап.Статус КАК Статус,
	|	ВТ_Заказ_Этап.Организация КАК Организация,
	|	ВТ_Заказ_Этап.Контрагент КАК Контрагент,
	|	ВТ_Заказ_Этап.Менеджер КАК Менеджер,
	|	ВТ_Заказ_Этап.Упаковка КАК Упаковка
	|ПОМЕСТИТЬ ВТ_ЭтапыПроизводства
	|ИЗ
	|	ВТ_Заказ_Этап КАК ВТ_Заказ_Этап
	|ГДЕ
	|	ВТ_Заказ_Этап.Отгрузка <> ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Заказ_Этап.Заказ,
	|	ВТ_Заказ_Этап.Отгрузка,
	|	ВТ_Заказ_Этап.Склад,
	|	ВТ_Заказ_Этап.Статус,
	|	ВТ_Заказ_Этап.Организация,
	|	ВТ_Заказ_Этап.Контрагент,
	|	ВТ_Заказ_Этап.Менеджер,
	|	ВТ_Заказ_Этап.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗаказыНаПроизводство.Заказ КАК Заказ,
	|	ВТ_ЗаказыНаПроизводство.Склад КАК Склад,
	|	ВТ_ЗаказыНаПроизводство.КоличествоЗаказ КАК КоличествоЗаказ,
	|	ВТ_ЗаказыНаПроизводство.Статус КАК Статус,
	|	ВТ_ЗаказыНаПроизводство.Организация КАК Организация,
	|	ВТ_ЗаказыНаПроизводство.Контрагент КАК Контрагент,
	|	ВТ_ЗаказыНаПроизводство.Менеджер КАК Менеджер,
	|	ВТ_ЗаказыНаПроизводство.Упаковка КАК Упаковка,
	|	ВТ_ЭтапыПроизводства.Отгрузка КАК Отгрузка,
	|	ВТ_ЭтапыПроизводства.КоличествоРТУ КАК КоличествоРТУ,
	|	ВТ_ЗаказыНаПроизводство.Состояние КАК Состояние
	|ПОМЕСТИТЬ ВТ_ЗаказыЭтапыИтог
	|ИЗ
	|	ВТ_ЗаказыНаПроизводство КАК ВТ_ЗаказыНаПроизводство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЭтапыПроизводства КАК ВТ_ЭтапыПроизводства
	|		ПО ВТ_ЗаказыНаПроизводство.Заказ = ВТ_ЭтапыПроизводства.Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Продажи.Заказ КАК Заказ,
	|	Продажи.Статус КАК Статус,
	|	Продажи.КоличествоЗаказ КАК КоличествоЗаказ,
	|	Продажи.Отменено КАК Отменено,
	|	Продажи.Склад КАК Склад,
	|	Продажи.Контрагент КАК Контрагент,
	|	Продажи.Организация КАК Организация,
	|	Продажи.Реализация КАК РеализацияПоступление,
	|	Продажи.КоличествоРТУ КАК КоличествоРТУ,
	|	Продажи.Партия КАК Партия,
	|	Продажи.Менеджер КАК Менеджер,
	|	Продажи.Состояние КАК Состояние,
	|	Продажи.Упаковка КАК Упаковка
	|ПОМЕСТИТЬ ВТ_Итоговая
	|ИЗ
	|	Продажи КАК Продажи
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Закупки.Заказ,
	|	Закупки.Статус,
	|	Закупки.КоличествоЗаказ,
	|	Закупки.Отменено,
	|	Закупки.Склад,
	|	Закупки.Контрагент,
	|	Закупки.Организация,
	|	Закупки.Регистратор,
	|	Закупки.КоличествоПТУ,
	|	Закупки.Партия,
	|	Закупки.Менеджер,
	|	Закупки.Состояние,
	|	Закупки.Упаковка
	|ИЗ
	|	Закупки КАК Закупки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ЗаказыЭтапыИтог.Заказ,
	|	ВТ_ЗаказыЭтапыИтог.Статус,
	|	ВТ_ЗаказыЭтапыИтог.КоличествоЗаказ,
	|	ЛОЖЬ,
	|	ВТ_ЗаказыЭтапыИтог.Склад,
	|	ВТ_ЗаказыЭтапыИтог.Контрагент,
	|	ВТ_ЗаказыЭтапыИтог.Организация,
	|	ВТ_ЗаказыЭтапыИтог.Отгрузка,
	|	ВТ_ЗаказыЭтапыИтог.КоличествоРТУ,
	|	ВТ_ЗаказыЭтапыИтог.Отгрузка,
	|	ВТ_ЗаказыЭтапыИтог.Менеджер,
	|	ВТ_ЗаказыЭтапыИтог.Состояние,
	|	ВТ_ЗаказыЭтапыИтог.Упаковка
	|ИЗ
	|	ВТ_ЗаказыЭтапыИтог КАК ВТ_ЗаказыЭтапыИтог
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка.Распоряжение,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка.Статус,
	|	0,
	|	ЛОЖЬ,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Склад,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка.Распоряжение.Организация,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.КоличествоУпаковок,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка.Распоряжение.Ответственный,
	|	СостоянияЗаказовНаПроизводство.СостояниеВыпуска,
	|	ВЫБОР
	|		КОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяССылка)
	|			ТОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Упаковка
	|	КОНЕЦ
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЗаказовНаПроизводство КАК СостоянияЗаказовНаПроизводство
	|		ПО (СостоянияЗаказовНаПроизводство.Заказ = ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка.Распоряжение)
	|ГДЕ
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка.Проведен
	|	И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка.Дата >= &ДатаНачала
	|	И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка.Дата <= &ДатаОкончания
	|	И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура = &Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Подтвержден),
	|	0,
	|	ЛОЖЬ,
	|	ПриобретениеТоваровУслугТовары.Склад,
	|	ПриобретениеТоваровУслугТовары.Ссылка.Контрагент,
	|	ПриобретениеТоваровУслугТовары.Ссылка.Организация,
	|	ПриобретениеТоваровУслугТовары.Ссылка,
	|	ПриобретениеТоваровУслугТовары.КоличествоУпаковок,
	|	ПриобретениеТоваровУслугТовары.Ссылка,
	|	ПриобретениеТоваровУслугТовары.Ссылка.Менеджер,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияЗаказовПоставщикам.Закрыт),
	|	ВЫБОР
	|		КОГДА ПриобретениеТоваровУслугТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяССылка)
	|			ТОГДА ПриобретениеТоваровУслугТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПриобретениеТоваровУслугТовары.Упаковка
	|	КОНЕЦ
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
	|ГДЕ
	|	ПриобретениеТоваровУслугТовары.Ссылка.Проведен
	|	И ПриобретениеТоваровУслугТовары.Ссылка.ЗаказПоставщику = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И ПриобретениеТоваровУслугТовары.Ссылка.Дата >= &ДатаНачала
	|	И ПриобретениеТоваровУслугТовары.Ссылка.Дата <= &ДатаОкончания
	|	И ПриобретениеТоваровУслугТовары.Номенклатура = &Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Итоговая.Заказ КАК Заказ,
	|	ВТ_Итоговая.Статус КАК Статус,
	|	ВТ_Итоговая.КоличествоЗаказ КАК КоличествоЗаказ,
	|	ВТ_Итоговая.Отменено КАК Отменено,
	|	ВТ_Итоговая.Склад КАК Склад,
	|	ВТ_Итоговая.Контрагент КАК Контрагент,
	|	ВТ_Итоговая.Организация КАК Организация,
	|	ВТ_Итоговая.РеализацияПоступление КАК РеализацияПоступление,
	|	СУММА(ВТ_Итоговая.КоличествоРТУ) КАК КоличествоРТУ,
	|	ВТ_Итоговая.Менеджер КАК Менеджер,
	|	ВТ_Итоговая.Состояние КАК Состояние,
	|	ВТ_Итоговая.Упаковка КАК Упаковка
	|ИЗ
	|	ВТ_Итоговая КАК ВТ_Итоговая
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Итоговая.Упаковка,
	|	ВТ_Итоговая.Отменено,
	|	ВТ_Итоговая.Заказ,
	|	ВТ_Итоговая.Статус,
	|	ВТ_Итоговая.Организация,
	|	ВТ_Итоговая.РеализацияПоступление,
	|	ВТ_Итоговая.Контрагент,
	|	ВТ_Итоговая.Менеджер,
	|	ВТ_Итоговая.Состояние,
	|	ВТ_Итоговая.Склад,
	|	ВТ_Итоговая.КоличествоЗаказ";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

// - [Rineco], [Киселев А.] [22.07.2021]



// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередЗагрузкойВариантаНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Подробнее - см. ОтчетыПереопределяемый.ПередЗагрузкойВариантаНаСервере
//
Процедура ПередЗагрузкойВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт
	

	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроекФормы = Отчет.КомпоновщикНастроек;

	// Изменение настроек по функциональным опциям
	НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы);

КонецПроцедуры

Процедура НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЕдиницыИзмеренияДляОтчетов") Тогда
		КомпоновкаДанныхСервер.УдалитьПараметрИзПользовательскихНастроекОтчета(КомпоновщикНастроекФормы, "ЕдиницыКоличества");
	КонецЕсли;
	
КонецПроцедуры


// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		Если Параметры.Свойство("ПараметрКоманды")
			И Параметры.ОписаниеКоманды.Свойство("ДополнительныеПараметры") Тогда
			
			Если Параметры.ОписаниеКоманды.ДополнительныеПараметры.ИмяКоманды = "ОборотыНоменклатуры" Тогда
				ЭтаФорма.ФормаПараметры.Отбор.Вставить("Номенклатура", Параметры.ПараметрКоманды);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры



