&НаКлиенте
Процедура ГИГ_ОбработкаВыбораПосле(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора <> Неопределено 
		И ТипЗнч(ИсточникВыбора) = Тип("УправляемаяФорма") 
		И ИсточникВыбора.ИмяФормы = "Справочник.гиг_ПредметыРасчета.ФормаВыбора" Тогда
		
		СоздатьЗадачуПроекта(ВыбранноеЗначение);
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГИГ_ПодборПредметовРасчетаПосле(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.гиг_ПредметыРасчета.ФормаВыбора", 
	ПараметрыФормы, 
	ЭтаФорма, 
	УникальныйИдентификатор,,,, 
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Процедура ГИГ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Владелец") И ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
		Проект = Параметры.Отбор.Владелец;
	Иначе
		ПолеВладелец = Новый ПолеКомпоновкиДанных("Владелец");
		Для каждого Элемент Из Список.Отбор.Элементы Цикл
			Если Элемент.ЛевоеЗначение = ПолеВладелец
				И ТипЗнч(Элемент.ПравоеЗначение) = Тип("СправочникСсылка.Проекты")
				Тогда
				Проект = Элемент.ПравоеЗначение;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Проект) Тогда
		ПроектРасчетИзделий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Проект, "гиг_РасчетИзделий");
	Иначе
		ПроектРасчетИзделий = Ложь;
	КонецЕсли;
	
	Элементы.ФормаПодборПредметовРасчета.Видимость = ПроектРасчетИзделий;
	ЭтаФорма.УстановитьДействие("ОбработкаВыбора", "ГИГ_ОбработкаВыбораПосле");
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЗадачуПроекта(ПредметРасчета)
	
	НоваяЗадача = Справочники.ЗадачиПроектов.СоздатьЭлемент();
	НоваяЗадача.Владелец = Проект;
	НоваяЗадача.Заполнить(Проект);
	НоваяЗадача.Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПредметРасчета, "Наименование");
	НоваяЗадача.ОписаниеЗадачи = НоваяЗадача.Наименование;
	
	//НоваяСтрока = НоваяЗадача.гиг_ЗначенияПараметров.Добавить();
	НоваяЗадача.ГИГ_ПредметРасчета = ПредметРасчета;
	
	НоваяЗадача.Записать();
	
КонецПроцедуры
