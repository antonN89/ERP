Процедура ВыполнитьЗапрос() Экспорт 
	
	пЕдИзмДлиныМетр =  Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду("006");
	пЕдИзмВесаКг = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду("166");
	пЕдИзмОбъемаМетр = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду("113");
	
	пЕдИзмДлиныДм =  Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду("005");
	пЕдИзмОбъемаДм = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду("112");
	
	пЕдИзмДлиныСм =  Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду("004");
	пЕдИзмОбъемаСм = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду("111");
	
	
	СтруктураПараметровПодключения = ПолучитьПараметрыПодключенияGET();
	Токен = СтруктураПараметровПодключения.access_token; 
	
	пФлагВсеПрочитано = Ложь;
	Страница = 1;
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		тзНоменклатура = ПолучитьНоменклатуруПоПроизводителю(Производитель);
	Иначе
		тзНоменклатура = Новый ТаблицаЗначений;
		тзНоменклатура.Колонки.Добавить("Артикул");
		тзНоменклатура.Колонки.Добавить("Ссылка");
		СтрТзНоменклатура = тзНоменклатура.Добавить();
		СтрТзНоменклатура.Артикул = Номенклатура.Артикул;
		СтрТзНоменклатура.Ссылка = Номенклатура.Ссылка;
	КонецЕсли;
	
	пСчетчик = 0;
	
	Для Каждого стрНоменклатура из тзНоменклатура Цикл 
		Ресурс = "v1/catalog/material?code=" + стрНоменклатура.Артикул;
		
		РезультатЗапросаДанных = ВыполнитьЗапросGet(Токен,Ресурс);
		
		Если РезультатЗапросаДанных.КодСостояния = 200 Тогда
			ПутьКФайлу = РезультатЗапросаДанных.ИмяФайлаОтвета;
			//прочитаем json
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.ОткрытьФайл(ПутьКФайлу);
			пОтвет = ПрочитатьJSON(ЧтениеJSON,Истина); //читаем в соответствие, т.к. у поставщика наименование некоторых атрибутов с прбелами и пр.символами, которые не могут присутствовать в наименованиях переменных
			ЧтениеJSON.Закрыть();
			
			Для Каждого Стр из пОтвет Цикл
				пКлассAPI = "";
				Для Каждого СтрПараметры из Стр.Значение Цикл   //Установим значение класса
					Если СтрПараметры.Ключ = "etim_class_id" Тогда
						пКлассAPI = СтрПараметры.Значение;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Для Каждого СтрПараметры из Стр.Значение Цикл
					Если СтрПараметры.Ключ = "etim_attributes" Тогда //установим значение свойств
                         Для Каждого СтрСвойстваКласса из СтрПараметры.Значение Цикл
								НоваяСтрока = Результат.Добавить();
								пСчетчик = пСчетчик + 1;
								НоваяСтрока.Артикул = стрНоменклатура.Артикул;
								НоваяСтрока.Номенклатура = стрНоменклатура.Ссылка;
								НоваяСтрока.КлассAPI = пКлассAPI;
								НоваяСтрока.СвойствоAPI = СтрСвойстваКласса.Ключ;
								НоваяСтрока.ЗначениеAPI = СтрСвойстваКласса.Значение;
								пНачалоДиапазона = СтрНайти(НоваяСтрока.ЗначениеAPI,"…"); //это не многоточие, какой-то символ, который использует DKC для разделения значений диапазона
								Если пНачалоДиапазона <> 0 Тогда
									 пДиапазон = НоваяСтрока.ЗначениеAPI;
									 пДлинаСтроки = СтрДлина(пДиапазон);
									 НоваяСтрока.Значение_R_API = СтрЗаменить(Лев(пДиапазон,пНачалоДиапазона - 1),".",",");
									 НоваяСтрока.ЗначениеR1_API =  СтрЗаменить(Прав(пДиапазон,пДлинаСтроки - пНачалоДиапазона),".",",");
									 НоваяСтрока.Value_RZ = НоваяСтрока.Value_R + "-" + НоваяСтрока.Value_R1;
								КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;						
								
			КонецЦикла;
		КонецЕсли;
		
		
		Если пСчетчик >= 500 Тогда //записываем порциями по 500 строк
			пРезультат = СопоставитьНоменклатуру(ЭтотОбъект.Результат.Выгрузить());
			ЭтотОбъект.Результат.Загрузить(пРезультат);
			ЗаписатьETIM();
			пСчетчик = 0;
		КонецЕсли;

		
	КонецЦикла;

	Если пСчетчик > 0 Тогда 
			пРезультат = СопоставитьНоменклатуру(ЭтотОбъект.Результат.Выгрузить());
			ЭтотОбъект.Результат.Загрузить(пРезультат);
			ЗаписатьETIM();
			пСчетчик = 0;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьНоменклатуруПоПроизводителю(пПроизводитель)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Номенклатура.Ссылка КАК Ссылка,
	                      |	Номенклатура.Артикул КАК Артикул
	                      |ИЗ
	                      |	Справочник.Номенклатура КАК Номенклатура
	                      |ГДЕ
	                      |	Номенклатура.Производитель = &Производитель");
	Запрос.УстановитьПараметр("Производитель",пПроизводитель);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ПолучитьДШВ(пСтрокаГабаритов)
	пСтруктураДШВ = Новый Структура;
	
	пПервыйРазделитель = СтрНайти(пСтрокаГабаритов,"*",,1);
	пВторойРазделитель = СтрНайти(пСтрокаГабаритов,"*",,пПервыйРазделитель+1);
	пДлинаСтроки = СтрДлина(пСтрокаГабаритов);
	пСтрокаГабаритов = СтрЗаменить(пСтрокаГабаритов,".",",");
	Если пПервыйРазделитель = 0 Тогда 
		пСтруктураДШВ.Вставить("Длина",0);
		пСтруктураДШВ.Вставить("Ширина",0);
		пСтруктураДШВ.Вставить("Высота",0);
	ИначеЕсли пВторойРазделитель = 0 Тогда 
		пВторойРазделитель = пДлинаСтроки + 1;
		пСтруктураДШВ.Вставить("Длина",Число(Лев(пСтрокаГабаритов, пПервыйРазделитель-1)));
		пСтруктураДШВ.Вставить("Ширина",Число(Сред(пСтрокаГабаритов, пПервыйРазделитель+1,пВторойРазделитель-пПервыйРазделитель-1)));
        пСтруктураДШВ.Вставить("Высота",0);
	Иначе 
		пСтруктураДШВ.Вставить("Длина",Число(Лев(пСтрокаГабаритов, пПервыйРазделитель-1)));
		пСтруктураДШВ.Вставить("Ширина",Число(Сред(пСтрокаГабаритов, пПервыйРазделитель+1,пВторойРазделитель-пПервыйРазделитель-1)));
		пСтруктураДШВ.Вставить("Высота",Число(Прав(пСтрокаГабаритов, пДлинаСтроки-пВторойРазделитель)));
	КонецЕсли;

	Возврат пСтруктураДШВ;
КонецФункции

Функция ВыполнитьЗапросGet(Токен,Ресурс) 
	РезультатЗапросаДанных = Новый Структура;
	
	Сервер = "api.dkc.ru";
	//Ресурс = "v1/catalog/material/stock?code=&id=";
	
	ssl1 = Новый ЗащищенноеСоединениеOpenSSL(
	Новый СертификатКлиентаWindows(),
	Новый СертификатыУдостоверяющихЦентровWindows());
	
	HTTP =  Новый HTTPСоединение(Сервер,,,,,,ssl1);
	
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла(".xml");
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json"); 
	ЗаголовокHTTP.Вставить("AccessToken", Токен);

	HTTPЗапрос = Новый HTTPЗапрос(Ресурс,ЗаголовокHTTP);
	
	ЗапросаРезультат = HTTP.Получить(HTTPЗапрос,ИмяФайлаОтвета);
	
	РезультатЗапросаДанных.Вставить("КодСостояния",ЗапросаРезультат.КодСостояния);
	РезультатЗапросаДанных.Вставить("ИмяФайлаОтвета",ИмяФайлаОтвета);
	
	Возврат РезультатЗапросаДанных;
	
КонецФункции	
	
Функция ПолучитьПараметрыПодключенияGET() 
	Сервер = "api.dkc.ru";
	Ресурс = "v1/auth.access.token/718e2e66ea41d51c8ca5c50018b8ff07";
	
	ssl1 = Новый ЗащищенноеСоединениеOpenSSL(
	Новый СертификатКлиентаWindows(),
	Новый СертификатыУдостоверяющихЦентровWindows());
	
	HTTP =  Новый HTTPСоединение(Сервер,,,,,,ssl1);
	
	ФайлРезультата = ПолучитьИмяВременногоФайла(".xml");
			
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type", "application/json"); 
		
	HTTPЗапрос = Новый HTTPЗапрос(Ресурс,ЗаголовокHTTP);
	
    HTTP.Получить(HTTPЗапрос,ФайлРезультата);
		
	//прочитаем json
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.ОткрытьФайл(ФайлРезультата);
	ПараметрыПодключения = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
		
	// Обнулим запрос, чтобы освободить чтение ИмяФайлаЗапроса!
	HTTPЗапрос = Неопределено;
	
	Попытка
		УдалитьФайлы(ФайлРезультата);
	Исключение
		ЗаписьЖурналаРегистрации("ФоновыеЗадания.РегламентноеЗаданиеAPI_ABB_stockall", УровеньЖурналаРегистрации.Информация, , , ОписаниеОшибки());
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;
		
	Возврат ПараметрыПодключения;

		
КонецФункции
Функция  СопоставитьНоменклатуру(Таблица) Экспорт 
	
	пЗапрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	Таблица.КлассAPI КАК КлассAPI,
|	Таблица.СвойствоAPI КАК СвойствоAPI,
|	Таблица.Артикул КАК Артикул,
|	Таблица.ЗначениеAPI КАК ЗначениеAPI,
|	Таблица.ЗначениеR1_API КАК ЗначениеR1_API,
|	Таблица.Значение_R_API КАК Значение_R_API,
|	Таблица.Value_RZ КАК Value_RZ,
|	Таблица.Номенклатура КАК Номенклатура
|ПОМЕСТИТЬ ТаблицаAPI
|ИЗ
|	&Таблица КАК Таблица
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ РАЗРЕШЕННЫЕ
|	ТаблицаAPI.Номенклатура КАК Номенклатура,
|	Рин1_ГруппыКлассыETIM.Ссылка КАК КлассСсылка,
|	ТаблицаAPI.Артикул КАК Артикул,
|	Рин1_ГруппыКлассыETIM.ПредставлениеГруппыКласса КАК КлассПредставление,
|	ТаблицаAPI.КлассAPI КАК КлассAPI,
|	ТаблицаAPI.СвойствоAPI КАК СвойствоAPI,
|	Рин1_ГруппыКлассыETIM.Родитель.ПредставлениеГруппыКласса КАК ГруппаПредставление,
|	ТаблицаAPI.ЗначениеAPI КАК ЗначениеAPI,
|	ТаблицаAPI.ЗначениеR1_API КАК ЗначениеR1_API,
|	ТаблицаAPI.Значение_R_API КАК Значение_R_API,
|	ТаблицаAPI.Value_RZ КАК Value_RZ
|ПОМЕСТИТЬ НоменклатураКлассETIM
|ИЗ
|	ТаблицаAPI КАК ТаблицаAPI
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Рин1_ГруппыКлассыETIM КАК Рин1_ГруппыКлассыETIM
|		ПО (ПОДСТРОКА(ТаблицаAPI.КлассAPI, 1, 100) = ПОДСТРОКА(Рин1_ГруппыКлассыETIM.Наименование, 1, 100))
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	НоменклатураКлассETIM.Номенклатура КАК Номенклатура,
|	НоменклатураКлассETIM.КлассПредставление КАК КлассПредставление,
|	НоменклатураКлассETIM.КлассAPI КАК КлассAPI,
|	НоменклатураКлассETIM.СвойствоAPI КАК СвойствоAPI,
|	Рин1_СвойстваКлассаETIM.Ссылка КАК Свойство,
|	Рин1_СвойстваКлассаETIM.ПредставлениеСвойства КАК СвойствоПредставление,
|	НоменклатураКлассETIM.ГруппаПредставление КАК ГруппаПредставление,
|	ВЫБОР
|		КОГДА Рин1_СвойстваКлассаETIM.ТИП = ЗНАЧЕНИЕ(Перечисление.Рин1_ТипСвойства.A)
|			ТОГДА НоменклатураКлассETIM.ЗначениеAPI
|		ИНАЧЕ """"
|	КОНЕЦ КАК Value_A,
|	ВЫБОР
|		КОГДА Рин1_СвойстваКлассаETIM.ТИП = ЗНАЧЕНИЕ(Перечисление.Рин1_ТипСвойства.N)
|			ТОГДА НоменклатураКлассETIM.ЗначениеAPI
|		ИНАЧЕ """"
|	КОНЕЦ КАК Value_N,
|	ВЫБОР
|		КОГДА Рин1_СвойстваКлассаETIM.ТИП = ЗНАЧЕНИЕ(Перечисление.Рин1_ТипСвойства.L)
|			ТОГДА НоменклатураКлассETIM.ЗначениеAPI
|		ИНАЧЕ """"
|	КОНЕЦ КАК Value_L,
|	ВЫБОР
|		КОГДА Рин1_СвойстваКлассаETIM.ТИП = ЗНАЧЕНИЕ(Перечисление.Рин1_ТипСвойства.R)
|			ТОГДА НоменклатураКлассETIM.Значение_R_API
|		ИНАЧЕ 0
|	КОНЕЦ КАК Value_R,
|	ВЫБОР
|		КОГДА Рин1_СвойстваКлассаETIM.ТИП = ЗНАЧЕНИЕ(Перечисление.Рин1_ТипСвойства.R)
|			ТОГДА НоменклатураКлассETIM.ЗначениеR1_API
|		ИНАЧЕ 0
|	КОНЕЦ КАК Value_R1,
|	НоменклатураКлассETIM.Value_RZ КАК Value_RZ
|ПОМЕСТИТЬ СвойстваETIM
|ИЗ
|	НоменклатураКлассETIM КАК НоменклатураКлассETIM
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Рин1_СвойстваКлассаETIM КАК Рин1_СвойстваКлассаETIM
|		ПО (Рин1_СвойстваКлассаETIM.Владелец = НоменклатураКлассETIM.КлассСсылка)
|			И (ПОДСТРОКА(НоменклатураКлассETIM.СвойствоAPI, 1, 100) = ПОДСТРОКА(Рин1_СвойстваКлассаETIM.Наименование, 1, 100))
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	СвойстваETIM.Номенклатура КАК Номенклатура,
|	СвойстваETIM.КлассПредставление КАК КлассПредставление,
|	СвойстваETIM.КлассAPI КАК КлассAPI,
|	СвойстваETIM.СвойствоAPI КАК СвойствоAPI,
|	СвойстваETIM.Свойство КАК Свойство,
|	СвойстваETIM.СвойствоПредставление КАК СвойствоПредставление,
|	СвойстваETIM.ГруппаПредставление КАК ГруппаПредставление,
|	Рин1_СвойстваКлассаETIMValue.ПредставлениеЗначения КАК Value_A,
|	СвойстваETIM.Value_N КАК Value_N,
|	СвойстваETIM.Value_L КАК Value_L,
|	СвойстваETIM.Value_R КАК Value_R,
|	СвойстваETIM.Value_R1 КАК Value_R1,
|	СвойстваETIM.Value_RZ КАК Value_RZ
|ИЗ
|	СвойстваETIM КАК СвойстваETIM
|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Рин1_СвойстваКлассаETIM.Value КАК Рин1_СвойстваКлассаETIMValue
|		ПО СвойстваETIM.Свойство = Рин1_СвойстваКлассаETIMValue.Ссылка
|			И СвойстваETIM.Value_A = Рин1_СвойстваКлассаETIMValue.Value_A
|ГДЕ
|	(СвойстваETIM.Value_A <> """"
|			ИЛИ СвойстваETIM.Value_N <> """"
|			ИЛИ СвойстваETIM.Value_L <> """"
|			ИЛИ СвойстваETIM.Value_RZ <> """")
|	И ЕСТЬNULL(СвойстваETIM.Номенклатура, 0) <> 0");
	
	пЗапрос.УстановитьПараметр("Таблица",Таблица);
	пЗапрос.УстановитьПараметр("Производитель",ЭтотОбъект.Производитель);

	Возврат пЗапрос.Выполнить().Выгрузить();	
КонецФункции

Процедура ЗаписатьETIM()  Экспорт
	пТаблица = ЭтотОбъект.Результат;
	
	Для Каждого Стр из пТаблица Цикл
		Если Не ЗначениеЗаполнено(Стр.Номенклатура) Тогда
			 Продолжить;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Стр.Номенклатура.КлассETIM) Тогда 
			пОбъектНоменклатура = Стр.Номенклатура.ПолучитьОбъект();
			пОбъектНоменклатура.КлассETIM = Стр.Свойство.Владелец;
			пОбъектНоменклатура.Записать();
		КонецЕсли;
			Запись = РегистрыСведений.Рин1_ЗначенияСвойствНоменклатуры.СоздатьМенеджерЗаписи();
			Запись.Номенклатура = Стр.Номенклатура;
			Запись.Свойство = Стр.Свойство;
			Запись.Прочитать();  
			ЗаполнитьЗначенияСвойств(Запись,Стр);
            Запись.Записать();
			
	КонецЦикла;
	ЭтотОбъект.Результат.Очистить();
КонецПроцедуры
	
Функция УдалитьЛишниеСимволы(пСтрока)
	
	КоличествоСимволов = СтрДлина(пСтрока);
	пПозиция = 1;
	
	Пока пПозиция <= КоличествоСимволов Цикл
		пКодСимвола = КодСимвола(пСтрока,пПозиция);
		пПозиция = пПозиция + 1;
		Если (пКодСимвола >= 48 и пКодСимвола <= 57) или пКодСимвола = 46 или пКодСимвола = 44 Тогда 
			Продолжить;
		Иначе
			пСтрока = СтрЗаменить(пСтрока,Символ(пКодСимвола),"");
		КонецЕсли;
	КонецЦикла;
	
	Возврат пСтрока;
КонецФункции

// Формирует и выводит сообщение, которое может быть связано с элементом 
// управления формы.
//
//  Параметры
//  ТекстСообщенияПользователю - Строка - Текст сообщения.
//  КлючДанных                 - ЛюбаяСсылка - на объект информационной базы.
//                               Ссылка на объект информационной базы, к которому это сообщение относится,
//                               или ключ записи.
//  Поле                       - Строка - Наименование реквизита формы.
//  ПутьКДанным                - Строка - Путь к данным (путь к реквизиту формы).
//  Отказ                      - Булево - Выходной параметр.
//                               Всегда устанавливается в значение Истина.
//  ИдентификаторНазначения    - УникальныйИдентификатор - Позволяет точно указать,
//                               к какой форме должно быть "привязано" сообщение.
//  ЭтоОбъект                  - Булево - Устанавливает на основе переданного объекта свойства ПутьКДанным и КлючДанных.
//
//	Пример:
//
//	1. Для вывода сообщения у поля управляемой формы, связанного с реквизитом объекта:
//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//		НСтр("ru = 'Сообщение об ошибке.'"), ,
//		"ПолеВРеквизитеФормыОбъект",
//		"Объект");
//
//	Альтернативный вариант использования в форме объекта:
//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//		НСтр("ru = 'Сообщение об ошибке.'"), ,
//		"Объект.ПолеВРеквизитеФормыОбъект");
//
//	2. Для вывода сообщения рядом с полем управляемой формы, связанным с реквизитом формы:
//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//		НСтр("ru = 'Сообщение об ошибке.'"), ,
//		"ИмяРеквизитаФормы");
//
//	3. Для вывода сообщения связанного с объектом информационной базы.
//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//		НСтр("ru = 'Сообщение об ошибке.'"), ОбъектИнформационнойБазы, "Ответственный",,Отказ);
//
// 4. Для вывода сообщения по ссылке на объект информационной базы.
//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
//		НСтр("ru = 'Сообщение об ошибке.'"), Ссылка, , , Отказ);
//
// Случаи некорректного использования:
//  1. Передача одновременно параметров КлючДанных и ПутьКДанным.
//  2. Передача в параметре КлючДанных значения типа отличного от допустимых.
//  3. Установка ссылки без установки поля (и/или пути к данным).
//
Процедура СообщитьПользователю(
	Знач ТекстСообщенияПользователю,
	Знач КлючДанных = Неопределено,
	Знач Поле = "",
	Знач ПутьКДанным = "",		
	Отказ = Ложь,
	Знач ИдентификаторНазначения = "",
	Знач ЭтоОбъект = Ложь)
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст		  = ТекстСообщенияПользователю;
	Сообщение.Поле		  = Поле;
	Сообщение.ПутьКДанным = ПутьКДанным;	
	
	Если ЭтоОбъект Тогда
		Сообщение.УстановитьДанные(КлючДанных);
	Иначе
		Сообщение.КлючДанных = КлючДанных;
	КонецЕсли;
	
	Если ТипЗнч(ИдентификаторНазначения) = Тип("УникальныйИдентификатор") Тогда
		Сообщение.ИдентификаторНазначения = ИдентификаторНазначения;
	КонецЕсли;
	
	Сообщение.Сообщить();
	
	Отказ = Истина;
	
КонецПроцедуры

