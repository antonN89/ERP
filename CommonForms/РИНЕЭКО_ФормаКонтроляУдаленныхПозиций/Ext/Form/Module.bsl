
&НаКлиенте
Процедура ВыделитьВсе(Команда)
	ВыделитьОтменить(Истина);	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьОтменить(ЗначениеПометки)
	
	Если ТаблицаТоваров.Количество() > 0 Тогда
		Для Каждого ЭлементТаблицы Из ТаблицаТоваров Цикл 
			ЭлементТаблицы.Отметка = ЗначениеПометки;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СнятьОтмеченные(Команда)
	ВыделитьОтменить(Ложь);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТаблицаТоваров.Загрузить(ПолучитьИзВременногоХранилища(Параметры.Адрес));
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	СтруктураВозврата = Новый Структура;
			
	СтруктураВозврата.Вставить("Адрес",ПеренестиДокументыСервер());
	СтруктураВозврата.Вставить("Владелец",ВладелецФормы);

	ЭтаФорма.Закрыть(СтруктураВозврата);

КонецПроцедуры


&НаСервере
Функция  ПеренестиДокументыСервер()
		
	ДанныеФормы = ДанныеФормыВЗначение(ТаблицаТоваров,Тип("ТаблицаЗначений"));
	ТаблицаРезультат = ДанныеФормы.СкопироватьКолонки();
	МассивСтрок = ДанныеФормы.НайтиСтроки(Новый Структура("Отметка",Истина));
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(МассивСтрок,ТаблицаРезультат);

	Если МассивСтрок.Количество() = 0 Тогда
		Адрес = Неопределено;
	Иначе 
		Адрес = ПоместитьВоВременноеХранилище(ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаРезультат));
	КонецЕсли;

	Возврат Адрес;
КонецФункции

&НаКлиенте
Процедура УказатьПричинуОтмены(Команда)
	Перем ЗначениеПричины;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ВводЗначениеЗавершение",ЭтотОбъект);	
	ПоказатьВводЗначения(ОповещениеОЗавершении,ЗначениеПричины,,Новый ОписаниеТипов("СправочникСсылка.ПричиныОтменыЗаказовПоставщикам, СправочникСсылка.ПричиныОтменыЗаказовКлиентов, СправочникСсылка.ПричиныОтменыПроизводства"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВводЗначениеЗавершение(Результат,Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаТоваров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементТовара Из ТаблицаТоваров Цикл 
		Если ЭлементТовара.Отметка Тогда
			ЭлементТовара.ПричинаОтмены = Результат;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
