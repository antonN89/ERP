
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПриСозданииНастроитьЭлементыФормы();
	
	ВосстановитьНастройки();
	
	УстановитьВидимостьФлагов(Параметры.СписокВыбора);
	УстановитьПорядок(ЭтаФорма);
	УстановитьПредставлениеФлагов(Параметры.СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьПодсказку();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтгрузитьПриИзменении(Элемент)
	
	ПриИзмененииФлага(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РезервироватьПриИзменении(Элемент)
	
	ПриИзмененииФлага(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбособленноПриИзменении(Элемент)
	
	ПриИзмененииФлага(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РезервироватьКСрокуПриИзменении(Элемент)
	
	ПриИзмененииФлага(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяПриИзменении(Элемент)
	
	ПриИзмененииФлага(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура НеТребуетсяПриИзменении(Элемент)
	
	ПриИзмененииФлага(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнятьЦелымиУпаковкамиПриИзменении(Элемент)
	СохранитьНастройки();
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	ПереченьВариантов = Новый Массив();
	Если Отгрузить Тогда
		
		Если Параметры.СписокВыбора = Неопределено
			Или Параметры.СписокВыбора.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить")) <> Неопределено Тогда
			
			ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить"));
			
		КонецЕсли;
		
		Если Параметры.СписокВыбора = Неопределено
			Или Параметры.СписокВыбора.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно")) <> Неопределено Тогда
			
			ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно"));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Резервировать Тогда
		ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.СоСклада"));
	КонецЕсли;
	
	Если РезервироватьКСроку Тогда
		ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ИзЗаказов"));
	КонецЕсли;
	
	Если Обособленно Тогда
		ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Обособленно"));
	КонецЕсли;
	
	Если Требуется Тогда
		ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Требуется"));
	КонецЕсли;
	
	Если НеТребуется Тогда
		ПереченьВариантов.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется"));
	КонецЕсли;
	
	Результат = Новый Структура("ПереченьВариантов, ЗаполнятьЦелымиУпаковками");
	Результат.ПереченьВариантов = ПереченьВариантов;
	Результат.ЗаполнятьЦелымиУпаковками = ЗаполнятьЦелымиУпаковками;
	
	Если Параметры.ПроверятьЗаполнение И ПереченьВариантов.Количество() = 0 Тогда
		
		Текст = НСтр("ru = 'Для заполнения необходимо установить хотя бы одну отметку';
					|en = 'To fill in, set at least one mark '");
		ПоказатьПредупреждение(, Текст);
		
	Иначе
		
		ОповеститьОВыборе(Результат);
		
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьПодсказку()

	НеобособленныеОстаткиНеУчитываютсяТекст = НСтр("ru = 'Фактические остатки необособленных товаров не анализируются.
	                                                     |Для заполнения по остаткам нужно указать второе действие,
	                                                     |которое будет установлено при отсутствии товара на складе:
	                                                     |к обеспечению, обеспечивать обособленно, либо не обеспечивать.';
	                                                     |en = 'Actual stock of not pegged goods is not analyzed.
	                                                     |To fill in according to remaining quantities, specify the second action 
	                                                     |which will be set if the goods are out of stock: 
	                                                     |to supply, provide on pegging basis or do not supply.'");
	ОстаткиНеУчитываютсяТекст = НСтр("ru = 'Фактические остатки товаров не анализируются.
	                                       |Для заполнения по остаткам нужно указать второе действие,
	                                       |которое будет установлено при отсутствии товара на складе:
	                                       |к обеспечению либо не обеспечивать.';
	                                       |en = 'Actual stock of not pegged goods is not analyzed.
	                                       |To fill in according to remaining quantities, 
	                                       |specify the second action which will be set if the goods are out of stock:
	                                       | to supply or do not supply.'");

	ЗаполнениеДопустимо  = Истина;
	ЗаполнениеПоОстаткам = Истина;

	// Варианты заполнения без учета остатков.
	Если Отгрузить И Не РезервироватьКСроку И Не Обособленно И Не Требуется И Не НеТребуется Тогда

		ЗаполнениеПоОстаткам = Ложь;
		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас - отгрузить обособленно.
			                       |Если ожидается обособленная поставка - обеспечивать обособленно.
			                       |Остальное количество - отгрузить.';
			                       |en = 'If there is separate inventory - ship on pegging basis.
			                       |If separate delivery is expected - provide on pegging basis.
			                       |Other quantity - ship.'");

			Предупреждение = НеобособленныеОстаткиНеУчитываютсяТекст;

		Иначе

			Подсказка = НСтр("ru = 'Все количество - отгрузить.';
							|en = 'All quantity - to ship.'");
			Предупреждение = ОстаткиНеУчитываютсяТекст;

		КонецЕсли;

	ИначеЕсли Резервировать И Не РезервироватьКСроку И Не Обособленно И Не Требуется И Не НеТребуется Тогда

		ЗаполнениеПоОстаткам = Ложь;
		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас или ожидается
			                       |обособленная поставка - обеспечивать обособленно.
			                       |Остальное количество - резервировать на складе.';
			                       |en = 'If there is pegged inventory or pegged delivery 
			                       |is expected - provide on pegging basis.
			                       |Other quantity - reserve in the warehouse.'");

			Предупреждение = НеобособленныеОстаткиНеУчитываютсяТекст;

		Иначе

			Подсказка = НСтр("ru = 'Все количество - резервировать на складе.';
							|en = 'All quantity - reserve at warehouse.'");
			Предупреждение = ОстаткиНеУчитываютсяТекст;

		КонецЕсли;

	ИначеЕсли РезервироватьКСроку И Не Обособленно И Не Требуется И Не НеТребуется Тогда

		ЗаполнениеПоОстаткам = Ложь;
		ЗаполнениеДопустимо  = Ложь;
		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Предупреждение = НСтр("ru = 'Выбор действия резервировать к дате возможен только
			                            |совместно с одним из действий: обеспечивать обособленно,
			                            |к обеспечению, не обеспечивать.';
			                            |en = 'You can select the ""Reserve to date"" action only
			                            | if you select one of the actions: supply separately, 
			                            |to supply, or do not supply.'");

		Иначе

			Предупреждение = НСтр("ru = 'Выбор действия резервировать к дате возможен только
			                            |совместно с одним из действий: к обеспечению, не обеспечивать.';
			                            |en = 'You can select the ""Reserve to a date"" action only
			                            | if you select one of the actions: to supply or do not supply.'");

		КонецЕсли;

	// Варианты заполнения с учетом остатков.
	ИначеЕсли Отгрузить И Не РезервироватьКСроку И Обособленно Тогда

		Подсказка = НСтр("ru = 'Если есть обособленный запас - отгрузить обособленно.
		                       |Если ожидается обособленная поставка - обеспечивать обособленно.
		                       |Если нет обособленного запаса, ожидаемой обособленной поставки
		                       |и товар есть на складе - отгрузить.
		                       |Остальное количество - обеспечивать обособленно.';
		                       |en = 'If there is pegged inventory - ship on pegging basis.
		                       |If pegged delivery is expected - provide on pegging basis.
		                       |If there is no pegged inventory, expected pegged delivery
		                       |and the goods are in stock - ship.
		                       |Other quantity - provide on pegging basis.'");

	ИначеЕсли Отгрузить И Не РезервироватьКСроку И Требуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас - отгрузить обособленно.
			                       |Если ожидается обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки
			                       |и товар есть на складе - отгрузить.
			                       |Остальное количество - к обеспечению.';
			                       |en = 'If there is pegged inventory - ship on pegging basis.
			                       |If pegged delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery
			                       |and the goods are in stock - ship.
			                       |Other quantity - for supply.'");

		Иначе

			Подсказка = НСтр("ru = 'Если товар в наличии на складе - отгрузить.
			                       |Остальное количество - к обеспечению.';
			                       |en = 'If the goods are in stock - ship.
			                       |Other quantity - for supply.'");

		КонецЕсли;

	ИначеЕсли Отгрузить И Не РезервироватьКСроку И НеТребуется Тогда


		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас - отгрузить обособленно.
			                       |Если ожидается обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки
			                       |и товар есть на складе - отгрузить.
			                       |Остальное количество - не обеспечивать.';
			                       |en = 'If there is pegged inventory - ship on pegging basis.
			                       |If pegged delivery is expected - provide on pegging basis.
			                       |If there are no pegged inventory, expected pegged delivery
			                       |and the goods are in stock - ship.
			                       |Other quantity - do not supply.'");

		Иначе

			Подсказка = НСтр("ru = 'Если товар в наличии на складе - отгрузить.
			                       |Остальное количество - не обеспечивать.';
			                       |en = 'If the goods are in stock - ship.
			                       |Other quantity - do not supply.'");

		КонецЕсли;

	ИначеЕсли Отгрузить И РезервироватьКСроку И Обособленно Тогда

		Подсказка = НСтр("ru = 'Если есть обособленный запас - отгрузить обособленно.
		                       |Если ожидается обособленная поставка - обеспечивать обособленно.
		                       |Если нет обособленного запаса, ожидаемой обособленной поставки
		                       |и товар есть на складе - отгрузить.
		                       |Если нет обособленного запаса, ожидаемой обособленной поставки,
		                       |товара на складе и ожидается поставка - резервировать по мере поступления.
		                       |Остальное количество - обеспечивать обособленно.';
		                       |en = 'If there is pegged inventory - ship on pegging basis.
		                       |If pegged delivery is expected - provide on pegging basis.
		                       |If there is no pegged inventory, expected pegged delivery
		                       |and the goods are in stock - ship.
		                       |If there is no pegged inventory, expected pegged delivery,
		                       |goods in stock and delivery is expected - reserve as soon as available.
		                       |Other quantity - provide on pegging basis.'");

	ИначеЕсли Отгрузить И РезервироватьКСроку И Требуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас - отгрузить обособленно.
			                       |Если ожидается обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки
			                       |и товар есть на складе - отгрузить.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки,
			                       |товара на складе и ожидается поставка - резервировать по мере поступления.
			                       |Остальное количество - принять к обеспечению.';
			                       |en = 'If there is pegged inventory - ship on pegging basis.
			                       |If pegged delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery
			                       |and the goods are in stock - ship.
			                       |If there is no pegged inventory, expected pegged delivery,
			                       |goods in stock and delivery is expected - reserve as soon as available.
			                       |Other quantity - accept for supply.'");

		Иначе
		
			Подсказка = НСтр("ru = 'Если есть в наличии - отгрузить.
		                    |Если нет в наличии, но ожидается поставка - резервировать к дате поступления.
		                    |Если нет в наличии и поставка не ожидается - принять к обеспечению.';
		                    |en = 'If in stock - ship.
		                    |If out of stock but delivery is expected - reserve to the receipt date.
		                    |If out of stock and delivery is not expected - accept for supply.'");

		КонецЕсли;

	ИначеЕсли Отгрузить И РезервироватьКСроку И НеТребуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас - отгрузить обособленно.
			                       |Если ожидается обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки
			                       |и товар есть на складе - отгрузить.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки,
			                       |товара на складе и ожидается поставка - резервировать по мере поступления.
			                       |Остальное количество - не обеспечивать.';
			                       |en = 'If there is pegged inventory - ship on pegging basis.
			                       |If pegged delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery
			                       |and the goods are in stock - ship.
			                       |If there is no pegged inventory, expected pegged delivery,
			                       |goods in stock and delivery is expected - reserve as soon as available.
			                       |Other quantity - do not supply.'");
			
		Иначе
		
			Подсказка = НСтр("ru = 'Если есть в наличии - отгрузить.
			                       |Если нет в наличии, но ожидается поставка - резервировать к дате поступления.
			                       |Если нет в наличии и поставка не ожидается - не обеспечивать.';
			                       |en = 'If in stock - ship.
			                       |If out of stock but delivery is expected - reserve to the receipt date.
			                       |If out of stock and delivery is not expected - do not supply.'");

		КонецЕсли;

	ИначеЕсли Резервировать И Не РезервироватьКСроку И Обособленно Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас или ожидается
			                       |обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки
			                       |и товар есть на складе - резервировать на складе.
			                       |Остальное количество - обеспечивать обособленно.';
			                       |en = 'If there is pegged inventory or pegged 
			                       |delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery
			                       |and the goods are in stock - reserve in the warehouse.
			                       |Other quantity - provide on pegging basis.'");

		Иначе

			Подсказка = НСтр("ru = 'Если есть в наличии - резервировать на складе.
			                       |Если нет в наличии - обеспечивать обособленно.';
			                       |en = 'If in stock - reserve in the warehouse.
			                       |If out of stock - provide on pegging basis.'");

		КонецЕсли;

	ИначеЕсли Резервировать И Не РезервироватьКСроку И Требуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас или ожидается
			                       |обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки
			                       |и товар есть на складе - резервировать на складе.
			                       |Остальное количество - принять к обеспечению.';
			                       |en = 'If there is pegged inventory or pegged 
			                       |delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery
			                       |and the goods are in stock - reserve in the warehouse.
			                       |Other quantity - accept for supply.'");

		Иначе

			Подсказка = НСтр("ru = 'Если есть в наличии - резервировать на складе.
			                       |Если нет в наличии - принять к обеспечению.';
			                       |en = 'If in stock - reserve in the warehouse.
			                       |If out of stock - accept for supply.'");

		КонецЕсли;

	ИначеЕсли Резервировать И Не РезервироватьКСроку И НеТребуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас или ожидается
			                       |обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки
			                       |и товар есть на складе - резервировать на складе.
			                       |Остальное количество - не обеспечивать.';
			                       |en = 'If there is pegged inventory or pegged 
			                       |delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery
			                       |and the goods are in stock - reserve in the warehouse.
			                       |Other quantity - do not supply.'");

		Иначе

			Подсказка = НСтр("ru = 'Если есть в наличии - резервировать на складе.
			                       |Если нет в наличии - не обеспечивать';
			                       |en = 'If in stock - reserve in the warehouse.
			                       |If out of stock - do not supply.'");

		КонецЕсли;

	ИначеЕсли Резервировать И РезервироватьКСроку И Не Обособленно И Не Требуется И Не НеТребуется Тогда

		ЗаполнениеПоОстаткам = Ложь;
		ЗаполнениеДопустимо  = Ложь;
		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Предупреждение = НСтр("ru = 'Выбор действия резервировать к дате возможен только
			                            |совместно с одним из действий: обеспечивать обособленно,
			                            |к обеспечению, не обеспечивать.';
			                            |en = 'You can select the ""Reserve to date"" action only
			                            | if you select one of the actions: supply separately, 
			                            |to supply, or do not supply.'");

		Иначе

			Предупреждение = НСтр("ru = 'Выбор действия резервировать к дате возможен только
			                            |совместно с одним из действий: к обеспечению, не обеспечивать.';
			                            |en = 'You can select the ""Reserve to a date"" action only
			                            | if you select one of the actions: to supply or do not supply.'");

		КонецЕсли;

	ИначеЕсли Резервировать И РезервироватьКСроку И Обособленно Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас или ожидается
			                       |обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки
			                       |и товар есть на складе - резервировать на складе.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки,
			                       |товара на складе и ожидается поставка - резервировать к дате поступления.
			                       |Остальное количество - обеспечивать обособленно.';
			                       |en = 'If there is pegged inventory or pegged 
			                       |delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery
			                       |and the goods are in stock - reserve in the warehouse.
			                       |If there is no pegged inventory, expected pegged delivery,
			                       |goods in stock and delivery is expected - reserve to the receipt date.
			                       |Other quantity - provide on pegging basis.'");

		Иначе

			Подсказка = НСтр("ru = 'Если есть в наличии - резервировать на складе.
			                       |Если нет в наличии, но ожидается поставка - резервировать к дате поступления.
			                       |Если нет в наличии и поставка не ожидается - обеспечивать обособленно.';
			                       |en = 'If in stock - reserve in the warehouse.
			                       |If out of stock but delivery is expected - reserve to the receipt date.
			                       |If out of stock and delivery is not expected - provide on pegging basis.'");

		КонецЕсли;

	ИначеЕсли Резервировать И РезервироватьКСроку И Требуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас или ожидается
			                       |обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки
			                       |и товар есть на складе - резервировать на складе.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки,
			                       |товара на складе и ожидается поставка - резервировать к дате поступления.
			                       |Остальное количество - принять к обеспечению.';
			                       |en = 'If there is pegged inventory or pegged 
			                       |delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery
			                       |and the goods are in stock - reserve in the warehouse.
			                       |If there is no pegged inventory, expected pegged delivery,
			                       |goods in stock and delivery is expected - reserve to the receipt date.
			                       |Other quantity - accept for supply.'");

		Иначе

			Подсказка = НСтр("ru = 'Если есть в наличии - резервировать на складе.
			                       |Если нет в наличии, но ожидается поставка - резервировать к дате поступления.
			                       |Если нет в наличии и поставка не ожидается - принять к обеспечению.';
			                       |en = 'If in stock - reserve in the warehouse.
			                       |If out of stock but delivery is expected - reserve to the receipt date.
			                       |If out of stock and delivery is not expected - accept for supply.'");

		КонецЕсли;

	ИначеЕсли Резервировать И РезервироватьКСроку И НеТребуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас или ожидается
			                       |обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки
			                       |и товар есть на складе - резервировать на складе.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки,
			                       |товара на складе и ожидается поставка - резервировать к дате поступления.
			                       |Остальное количество - не обеспечивать.';
			                       |en = 'If there is pegged inventory or pegged 
			                       |delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery
			                       |and the goods are in stock - reserve in the warehouse.
			                       |If there is no pegged inventory, expected pegged delivery,
			                       |goods in stock and delivery is expected - reserve to the receipt date.
			                       |Other quantity - do not supply.'");

		Иначе

			Подсказка = НСтр("ru = 'Если есть в наличии - резервировать на складе.
			                       |Если нет в наличии, но ожидается поставка - резервировать к дате поступления.
			                       |Если нет в наличии и поставка не ожидается - не обеспечивать.';
			                       |en = 'If in stock - reserve in the warehouse.
			                       |If out of stock but delivery is expected - reserve to the receipt date.
			                       |If out of stock and delivery is not expected - do not supply.'");

		КонецЕсли;

	ИначеЕсли РезервироватьКСроку И Обособленно Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас или ожидается
			                       |обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки и товар
			                       |есть на складе или ожидается поставка - резервировать к дате поступления.
			                       |Остальное количество - обеспечивать обособленно.';
			                       |en = 'If there is pegged inventory or pegged 
			                       |delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery and the goods are 
			                       |in stock or delivery is expected - reserve to the receipt date.
			                       |Other quantity - provide on pegging basis.'");

		Иначе

			Подсказка = НСтр("ru = 'Если есть в наличии или ожидается поставка - резервировать к дате поступления.
			                       |Если есть в наличии и поставка не ожидается - обеспечивать обособленно.';
			                       |en = 'If in stock or delivery is expected - reserve to the receipt date.
			                       |If out of stock and delivery is not expected - provide on pegging basis.'");

		КонецЕсли;

	ИначеЕсли РезервироватьКСроку И Требуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас или ожидается
			                       |обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки и товар
			                       |есть на складе или ожидается поставка - резервировать к дате поступления.
			                       |Остальное количество - принять к обеспечению.';
			                       |en = 'If there is pegged inventory or pegged 
			                       |delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery and the goods
			                       |are in stock or delivery is expected - reserve to the receipt date.
			                       |Other quantity - accept for supply.'");

		Иначе

			Подсказка = НСтр("ru = 'Если есть в наличии или ожидается поставка - резервировать к дате поступления.
			                       |Если есть в наличии и поставка не ожидается - принять к обеспечению.';
			                       |en = 'If in stock or delivery is expected - reserve to the receipt date.
			                       |If out of stock and delivery is not expected - accept for supply.'");

		КонецЕсли;

	ИначеЕсли РезервироватьКСроку И НеТребуется Тогда

		Если Элементы.ГруппаОбособленно.Видимость Тогда // используется обособленное обеспечение заказов.

			Подсказка = НСтр("ru = 'Если есть обособленный запас или ожидается
			                       |обособленная поставка - обеспечивать обособленно.
			                       |Если нет обособленного запаса, ожидаемой обособленной поставки и товар
			                       |есть на складе или ожидается поставка - резервировать к дате поступления.
			                       |Остальное количество - не обеспечивать.';
			                       |en = 'If there is pegged inventory or pegged 
			                       |delivery is expected - provide on pegging basis.
			                       |If there is no pegged inventory, expected pegged delivery and the goods are 
			                       |in stock or delivery is expected - reserve to the receipt date.
			                       |Other quantity - do not supply.'");

		Иначе

			Подсказка = НСтр("ru = 'Если есть в наличии или ожидается поставка - резервировать к дате поступления.
			                       |Если есть в наличии и поставка не ожидается - не обеспечивать.';
			                       |en = 'If in stock or delivery is expected - reserve to the receipt date.
			                       |If out of stock and delivery is not expected - do not supply.'");

		КонецЕсли;

	ИначеЕсли Обособленно Тогда

		Подсказка = НСтр("ru = 'Все строки - обеспечивать обособленно.';
						|en = 'All lines - provide on pegging basis.'");

	ИначеЕсли Требуется Тогда

		Подсказка = НСтр("ru = 'Все строки - принять к обеспечению.';
						|en = 'All lines - accept for supply.'");

	ИначеЕсли НеТребуется Тогда

		Подсказка = НСтр("ru = 'Все строки - не обеспечивать.';
						|en = 'All lines - do not supply.'");
		
	Иначе

		Если Параметры.СписокВыбора <> Неопределено Тогда

			Подсказка = НСтр("ru = 'Не выбраны действия для заполнения.
			            |Необходимо установить отметку/отметки.';
			            |en = 'Actions for filling are not selected.
			            | Set mark(s).'");

		Иначе

			Подсказка = НСтр("ru = 'Не выбраны действия для заполнения.
			            |Необходимо установить отметки (не более трех).';
			            |en = 'Actions for population are not selected. 
			            |Set marks (not more than three).'");

		КонецЕсли;

	КонецЕсли;

	Элементы.СтраницыПодсказки.ТекущаяСтраница = ?(ЗаполнениеПоОстаткам, Элементы.СтраницаПодсказкаПоОстаткам,
	                                             ?(ЗаполнениеДопустимо,  Элементы.СтраницаПодсказкаБезОстатков,
	                                                                     Элементы.СтраницаНедопустимыйВариант));

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииФлага(Элемент)
	
	Отгрузить = ?(Элемент = Элементы.Резервировать, Ложь, Отгрузить);
	
	Резервировать = ?(Элемент = Элементы.Отгрузить, Ложь, Резервировать);
	
	Обособленно = ?(Элемент = Элементы.Требуется Или Элемент = Элементы.НеТребуется, Ложь, Обособленно);
	
	Требуется = ?(Элемент = Элементы.Обособленно Или Элемент = Элементы.НеТребуется, Ложь, Требуется);
	
	НеТребуется = ?(Элемент = Элементы.Обособленно Или Элемент = Элементы.Требуется, Ложь, НеТребуется);
	
	УстановитьПорядок(ЭтаФорма);
	УстановитьПодсказку();
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьФлагов(Список)

	Если Параметры.СписокВыбора <> Неопределено Тогда

		Элементы.ГруппаОтгрузить.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Отгрузить) <> Неопределено
			Или Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.ОтгрузитьОбособленно) <> Неопределено;

		Элементы.ГруппаРезервировать.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.СоСклада) <> Неопределено;

		Элементы.ГруппаРезервироватьКСроку.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.ИзЗаказов) <> Неопределено;

		Элементы.ГруппаОбособленно.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Обособленно) <> Неопределено;

		Элементы.ГруппаТребуется.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Требуется) <> Неопределено;

		Элементы.ГруппаНеТребуется.Видимость =
			Список.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.НеТребуется) <> Неопределено;

	КонецЕсли;

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбособленноеОбеспечениеЗаказов") Тогда

		Элементы.ГруппаОбособленно.Видимость = Ложь;

	Иначе

		Элементы.Отгрузить.Заголовок = НСтр("ru = 'Отгрузить (при необходимости обособленно)';
											|en = 'Ship (on pegging basis if needed)'");

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеФлагов(Список)
	
	Если Список = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ЭлементСписка Из Список Цикл
		Если НЕ ЗначениеЗаполнено(ЭлементСписка.Представление) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.Отгрузить Тогда
			Элементы.Отгрузить.Заголовок = ЭлементСписка.Представление;
		ИначеЕсли ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.СоСклада Тогда
			Элементы.Резервировать.Заголовок = ЭлементСписка.Представление;
		ИначеЕсли ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.ИзЗаказов Тогда
			Элементы.РезервироватьКСроку.Заголовок = ЭлементСписка.Представление;
		ИначеЕсли ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.Обособленно Тогда
			Элементы.Обособленно.Заголовок = ЭлементСписка.Представление;
		ИначеЕсли ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.Требуется Тогда
			Элементы.Требуется.Заголовок = ЭлементСписка.Представление;
		ИначеЕсли ЭлементСписка.Значение = Перечисления.ВариантыОбеспечения.НеТребуется Тогда
			Элементы.НеТребуется.Заголовок = ЭлементСписка.Представление;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПорядок(ЭтаФорма)

	СчетчикРезервироватьКСроку = 0;
	СчетчикНеобеспечено        = 0;

	Если ЭтаФорма.Отгрузить Тогда

		СчетчикРезервироватьКСроку = СчетчикРезервироватьКСроку + 1;
		СчетчикНеобеспечено        = СчетчикНеобеспечено + 1;

		ЭтаФорма.ОтгрузитьПорядок = 1;

	Иначе

		ЭтаФорма.ОтгрузитьПорядок = 0;

	КонецЕсли;

	Если ЭтаФорма.Резервировать Тогда

		СчетчикРезервироватьКСроку = СчетчикРезервироватьКСроку + 1;
		СчетчикНеобеспечено        = СчетчикНеобеспечено + 1;

		ЭтаФорма.РезервироватьПорядок = 1;

	Иначе

		ЭтаФорма.РезервироватьПорядок = 0;

	КонецЕсли;

	Если ЭтаФорма.РезервироватьКСроку Тогда

		СчетчикНеобеспечено = СчетчикНеобеспечено + 1;

		ЭтаФорма.РезервироватьКСрокуПорядок = 1 + СчетчикРезервироватьКСроку;

	Иначе

		ЭтаФорма.РезервироватьКСрокуПорядок = 0;

	КонецЕсли;

	ЭтаФорма.ОбособленноПорядок = ?(ЭтаФорма.Обособленно, 1 + СчетчикНеобеспечено, 0);
	ЭтаФорма.ТребуетсяПорядок   = ?(ЭтаФорма.Требуется,   1 + СчетчикНеобеспечено, 0);
	ЭтаФорма.НеТребуетсяПорядок = ?(ЭтаФорма.НеТребуется, 1 + СчетчикНеобеспечено, 0);

КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	Настройки = Новый Структура("ИзЗаказов, СоСклада, Обособленно, Отгрузить, Требуется, НеТребуется, ЗаполнятьЦелымиУпаковками");
	Настройки.ИзЗаказов   = РезервироватьКСроку;
	Настройки.СоСклада    = Резервировать;
	Настройки.Обособленно = Обособленно;
	Настройки.Отгрузить   = Отгрузить;
	Настройки.Требуется   = Требуется;
	Настройки.НеТребуется = НеТребуется;
	
	Настройки.ЗаполнятьЦелымиУпаковками = ЗаполнятьЦелымиУпаковками;
	
	Шаблон = НСтр("ru = 'Заполнение варианта обеспечения %1';
					|en = 'Fill in the %1 supply option'");
	КлючНастройки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Параметры.Тип);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Перечисление.ВариантыОбеспечения", КлючНастройки, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройки()
	
	//Шаблон = НСтр("ru = 'Заполнение варианта обеспечения %1';
	//				|en = 'Fill in the %1 supply option'");
	//КлючНастройки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Параметры.Тип);
	
	//ЗначениеНастроек = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Перечисление.ВариантыОбеспечения", КлючНастройки);
	
	ЗначениеНастроек = Новый Структура;
	ЗначениеНастроек.Вставить("СоСклада",Истина);
	ЗначениеНастроек.Вставить("ИзЗаказов",Истина);
	ЗначениеНастроек.Вставить("Требуется",Истина);
	ЗначениеНастроек.Вставить("ЗаполнятьЦелымиУпаковками",Истина);
	
	Если ТипЗнч(ЗначениеНастроек) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ЗначениеНастроек);
		РезервироватьКСроку = ЗначениеНастроек.ИзЗаказов;
		Резервировать = ЗначениеНастроек.СоСклада;
		
	КонецЕсли;
	
	Если Параметры.Свойство("РазрешитьКонтрольЦелостностиУпаковок") И Не Параметры.РазрешитьКонтрольЦелостностиУпаковок Тогда
		ЗаполнятьЦелымиУпаковками = Ложь;
	КонецЕсли;
	
	Если Параметры.СписокВыбора <> Неопределено Тогда
		
		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.ИзЗаказов) = Неопределено Тогда
			РезервироватьКСроку = Ложь;
		КонецЕсли;
		
		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.СоСклада) = Неопределено Тогда
			Резервировать = Ложь;
		КонецЕсли;
		
		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Обособленно) = Неопределено Тогда
			Обособленно = Ложь;
		КонецЕсли;
		
		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Отгрузить) = Неопределено Тогда
			Отгрузить = Ложь;
		КонецЕсли;
		
		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.Требуется) = Неопределено Тогда
			Требуется = Ложь;
		КонецЕсли;
		
		Если Параметры.СписокВыбора.НайтиПоЗначению(Перечисления.ВариантыОбеспечения.НеТребуется) = Неопределено Тогда
			НеТребуется = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНастроитьЭлементыФормы()
	
	Если Параметры.Свойство("ЗаголовокФормы")
		И Параметры.ЗаголовокФормы <> Неопределено Тогда 
		ЭтаФорма.Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокКомандыЗаполнить")
		И Параметры.ЗаголовокКомандыЗаполнить <> Неопределено Тогда
		Элементы.Заполнить.Заголовок = Параметры.ЗаголовокКомандыЗаполнить;
	КонецЕсли;
	
	Если Параметры.Свойство("РазрешитьКонтрольЦелостностиУпаковок") Тогда
		Элементы.ГруппаЗаполнятьЦелымиУпаковками.Видимость = Параметры.РазрешитьКонтрольЦелостностиУпаковок;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


