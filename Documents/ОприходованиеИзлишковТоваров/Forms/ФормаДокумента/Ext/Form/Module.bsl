
&НаКлиенте
Процедура Рин1_ЗаполнитьНомераГТДПоПоступлениямПеред(Команда)
	ЗаполнитьНомераГТДПоПоступлениям();
КонецПроцедуры
	
&НаКлиенте
Процедура ЗаполнитьНомераГТДПоПоступлениям()
	ЗаполненыНомераГТД = Ложь;
	ЗаполнитьНомераГТДПоПоследнимПоступлениямСервер(ЗаполненыНомераГТД);
	
	Если ЗаполненыНомераГТД Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Номера ГТД заполнены';
				|en = 'CCD numbers are filled in'"),
			,
			НСтр("ru = 'В строках документа заполнены номера ГТД';
				|en = 'CCD numbers are populated in the document lines'"),
			БиблиотекаКартинок.Информация32);
	Иначе
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Номера ГТД не заполнены';
				|en = 'CCD numbers are not filled in'"),
			,
			НСтр("ru = 'Ни в одной строке номер ГТД не заполнен';
				|en = 'CCD number is not filled in in any lines'"),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНомераГТДПоПоследнимПоступлениямСервер(ЗаполненыНомераГТД)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.НомерСтроки КАК НомерСтроки,
	|	ИсходнаяТаблица.Номенклатура КАК Номенклатура
	|
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|ГДЕ
	//|	ИсходнаяТаблица.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	//|	И ИсходнаяТаблица.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	ИсходнаяТаблица.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ИсходнаяТаблица.ВедетсяУчетПоГТД
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ДанныеРегистра.ДатаПоступления) КАК ДатаПоступления
	|	
	|ПОМЕСТИТЬ ДатыПоступленияТоваровОрганизаций
	|ИЗ
	|	РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Номенклатура
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ДанныеРегистра.НомерГТД) КАК НомерГТД	
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДатыПоступленияТоваровОрганизаций КАК ДатыПоступленияТоваровОрганизаций
	|	ПО
	|		ТаблицаТовары.Номенклатура = ДатыПоступленияТоваровОрганизаций.Номенклатура
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ДанныеРегистра
	|	ПО
	|		ТаблицаТовары.Номенклатура = ДанныеРегистра.Номенклатура
	|		И ДанныеРегистра.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|		И ДатыПоступленияТоваровОрганизаций.ДатаПоступления = ДанныеРегистра.ДатаПоступления
	|		
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.НомерСтроки,
	|	ТаблицаТовары.Номенклатура
	|");
	Запрос.УстановитьПараметр("ИсходнаяТаблица", Объект.Товары.Выгрузить(,"НомерСтроки, Номенклатура, НомерГТД, ТипНоменклатуры, ВедетсяУчетПОГТД"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаТаблицы = Объект.Товары.Получить(Выборка.НомерСтроки - 1);
		СтрокаТаблицы.НомерГТД = Выборка.НомерГТД;
		
		Действия = Новый Структура;
		Действия.Вставить("ЗаполнитьСтрануПроисхожденияДляНомераГТД", Новый Структура("НомерГТД", Выборка.НомерГТД));
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТаблицы, Действия, Неопределено);
		
		ЗаполненыНомераГТД = Истина;
		
	КонецЦикла;
	
КонецПроцедуры