// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СформироватьДерево(Параметры);
	
КонецПроцедуры

// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаСервере
Процедура СформироватьДерево(Параметры)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура КАК Номенклатура,
		|	СУММА(ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Количество) КАК ИтоговоеКоличество,
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Склад КАК Склад,
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура.ЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
		|	СУММА(0) КАК КоличествоВУпаковке,
		|	СУММА(0) КАК КоличествоЦелых,
		|	СУММА(0) КАК ДробныйОстаток,
		|	СУММА(0) КАК РазницаДляРаспределения
		|ПОМЕСТИТЬ ВТ_Материалы
		|ИЗ
		|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами
		|ГДЕ
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка В(&МассивЭтапов)
		|	И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.СтатьяКалькуляции В(&МассивСтатей)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура,
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Склад,
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура.ЕдиницаИзмерения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЭтапПроизводства2_2.Ссылка КАК Документ,
		|	ЭтапПроизводства2_2.Этап КАК Этап,
		|	ЭтапПроизводства2_2.Подразделение КАК Подразделение,
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура.Артикул КАК Артикул,
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура КАК Наименование,
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Количество КАК Обеспечение,
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Склад КАК Склад,
		|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Спецификация КАК Спецификация,
		|	ЭтапПроизводства2_2.Запланировано КАК Количество,
		|	ЛОЖЬ КАК Отметка
		|ИЗ
		|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
		|		ПО ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка = ЭтапПроизводства2_2.Ссылка
		|ГДЕ
		|	ЭтапПроизводства2_2.Ссылка В(&МассивЭтапов)
		|	И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.СтатьяКалькуляции В(&МассивСтатей)
		|ИТОГИ ПО
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Материалы.Номенклатура КАК Материал,
		|	ВТ_Материалы.ИтоговоеКоличество КАК ИтоговоеКоличество,
		|	ВТ_Материалы.Склад КАК Склад,
		|	ВТ_Материалы.НоменклатураЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВТ_Материалы.ДробныйОстаток КАК ДробныйОстаток,
		|	ВТ_Материалы.РазницаДляРаспределения КАК РазницаДляРаспределения,
		|	МИНИМУМ(УпаковкиЕдиницыИзмерения.Числитель) КАК КоличествоВУпаковке,
		|	ВТ_Материалы.КоличествоЦелых КАК КоличествоЦелых,
		|	УпаковкиЕдиницыИзмерения.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_ИтоговаяСЧислителем
		|ИЗ
		|	ВТ_Материалы КАК ВТ_Материалы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
		|		ПО ВТ_Материалы.Номенклатура = УпаковкиЕдиницыИзмерения.Владелец
		|ГДЕ
		|	УпаковкиЕдиницыИзмерения.Ссылка <> ВТ_Материалы.Номенклатура.ЕдиницаИзмерения
		|	И УпаковкиЕдиницыИзмерения.Числитель <> 1
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Материалы.НоменклатураЕдиницаИзмерения,
		|	ВТ_Материалы.Номенклатура,
		|	ВТ_Материалы.Склад,
		|	ВТ_Материалы.ИтоговоеКоличество,
		|	ВТ_Материалы.КоличествоВУпаковке,
		|	ВТ_Материалы.ДробныйОстаток,
		|	ВТ_Материалы.РазницаДляРаспределения,
		|	ВТ_Материалы.КоличествоЦелых,
		|	УпаковкиЕдиницыИзмерения.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ИтоговаяСЧислителем.Материал КАК Материал,
		|	ВТ_ИтоговаяСЧислителем.ИтоговоеКоличество КАК ИтоговоеКоличество,
		|	ВТ_ИтоговаяСЧислителем.Склад КАК Склад,
		|	ВТ_ИтоговаяСЧислителем.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВТ_ИтоговаяСЧислителем.ДробныйОстаток КАК ДробныйОстаток,
		|	ВТ_ИтоговаяСЧислителем.РазницаДляРаспределения КАК РазницаДляРаспределения,
		|	ВТ_ИтоговаяСЧислителем.КоличествоВУпаковке КАК КоличествоВУпаковке,
		|	ВЫРАЗИТЬ(ВТ_ИтоговаяСЧислителем.ИтоговоеКоличество / ВТ_ИтоговаяСЧислителем.КоличествоВУпаковке КАК ЧИСЛО(15, 0)) КАК КоличествоЦелых,
		|	ИСТИНА КАК Отметка,
		|	ВТ_ИтоговаяСЧислителем.Ссылка КАК Ссылка
		|ИЗ
		|	ВТ_ИтоговаяСЧислителем КАК ВТ_ИтоговаяСЧислителем";
	
	Запрос.УстановитьПараметр("МассивЭтапов", Параметры.МассивЭтапов);
	Запрос.УстановитьПараметр("МассивСтатей", Параметры.МассивМассивСтатейКалькуляции);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаДокумент = РезультатЗапроса[1].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЗначениеВДанныеФормы(ВыборкаДокумент,ДеревоЭтапов);
	
	ТаблицаМатериалы = РезультатЗапроса[3].Выгрузить();
	ЗначениеВРеквизитФормы(ТаблицаМатериалы,"ТаблицаРасчета");
	РассчитатьОстатки();
	
КонецПроцедуры

// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаСервере
Процедура РассчитатьОстатки()
	
	Если ТаблицаРасчета.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементТаблицы Из ТаблицаРасчета Цикл 
		
		ЭлементТаблицы.КоличествоЦелых = Цел(ЭлементТаблицы.ИтоговоеКоличество / ЭлементТаблицы.КоличествоВУпаковке);
		ЭлементТаблицы.ДробныйОстаток = ЭлементТаблицы.КоличествоЦелых * ЭлементТаблицы.КоличествоВУпаковке - ЭлементТаблицы.ИтоговоеКоличество;
		ЭлементТаблицы.РазницаДляРаспределения = ЭлементТаблицы.ДробныйОстаток + ЭлементТаблицы.КоличествоВУпаковке;
		
	КонецЦикла;
	
КонецПроцедуры

// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаКлиенте
Процедура ТаблицаРасчетаКоличествоЦелыхПриИзменении(Элемент)
	РассчитатьОстатки();
КонецПроцедуры

// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаКлиенте
Процедура УстановитьОтметкуМатериалов(Команда)
	УстановитьСнятьОтметку(Истина);
КонецПроцедуры

// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаСервере
Процедура УстановитьСнятьОтметку(Значение)
	Для Каждого ЭлементМатериала Из ТаблицаРасчета Цикл 
		ЭлементМатериала.Отметка = Значение;
	КонецЦикла;
КонецПроцедуры

// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаСервере
Процедура УстановитьСнятьОтметкуДерева(Значение)
	Для Каждого ЭлементДерева Из ДеревоЭтапов.ПолучитьЭлементы() Цикл 
		ЭлементДерева.Отметка = Значение;
	КонецЦикла;
КонецПроцедуры


// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаКлиенте
Процедура СнятьОтметкуМатериалов(Команда)
	УстановитьСнятьОтметку(Ложь);
КонецПроцедуры

// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаКлиенте
Процедура СнятьОтметкуДерева(Команда)
	УстановитьСнятьОтметкуДерева(Ложь);
КонецПроцедуры

// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаКлиенте
Процедура УстановитьОтметкуДерева(Команда)
	УстановитьСнятьОтметкуДерева(Истина);
КонецПроцедуры

// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаКлиенте
Процедура РаспределитьВыделенное(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		РаспределитьВыделенноеНаСервере();
	КонецЕсли;
	
КонецПроцедуры

// + [Rineco], [Киселев А.] [27.12.2021] [Log_Diff][№ 24459], [#Распредение в этапах] 
&НаСервере
Процедура РаспределитьВыделенноеНаСервере()
	
	МассивОтмеченныхМатериалов = Новый Массив;
	
	Для Каждого ЭлементТаблицы Из ТаблицаРасчета Цикл 
		Если ЭлементТаблицы.Отметка Тогда
			МассивОтмеченныхМатериалов.Добавить(ЭлементТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивОтмеченныхМатериалов.Количество() = 0 Тогда
		Сообщить("Нет отмеченных материалов");
		Возврат;
	КонецЕсли;
	
	МассивРаспределяемыхЭтапов = Новый Массив;
	Для Каждого ЭлементЭтапа Из ДеревоЭтапов.ПолучитьЭлементы() Цикл 
		МассивРаспределяемыхЭтапов.Добавить(ЭлементЭтапа);
		Прервать;
	КонецЦикла;
	
	Если МассивРаспределяемыхЭтапов.Количество() = 0 Тогда
		Сообщить("Нет отмеченных этапов");
		Возврат;
	КонецЕсли;
	
	Для Каждого Этап Из МассивРаспределяемыхЭтапов Цикл 
		
		Для Каждого Материал Из МассивОтмеченныхМатериалов Цикл 
			ЭтапОбъект = Этап.Документ.ПолучитьОбъект();
			НоваяСтрока = ЭтапОбъект.ОбеспечениеМатериаламиИРаботами.Добавить();
			НоваяСтрока.Номенклатура = Материал.Материал;
			НоваяСтрока.Количество = Материал.РазницаДляРаспределения;
			НоваяСтрока.КоличествоУпаковок = Материал.РазницаДляРаспределения;
			НоваяСтрока.Склад = Материал.Склад;
			НоваяСтрока.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Требуется;
			НоваяСтрока.ДатаОтгрузки = ТекущаяДата();
			НоваяСтрока.СтатьяКалькуляции = СтатьяДляУстановки;
			
			ЭтапОбъект.Записать();
			Сообщить("Распределение материала: " + Материал.Материал + " завершено");
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры
