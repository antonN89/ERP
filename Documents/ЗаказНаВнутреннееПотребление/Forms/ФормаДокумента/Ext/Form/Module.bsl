&НаКлиенте
Процедура ГИГ_ГИГ_ОЕМПотребностьПриИзменении(Элемент) //ОЕМПотребность
	
	ГИГ_ОЕМПотребностьУстановитьВидимостьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ГИГ_ОЕМПотребностьУстановитьВидимостьНаСервере() //ОЕМПотребность
	
	Элементы.ГИГ_Партнер.Видимость = Объект.ГИГ_ОЕМПотребность;
	Элементы.ГИГ_ПредставлениеОЕМПотребности.Видимость = Объект.ГИГ_ОЕМПотребность;
//{{20201005 ГлазуновДВ
	Элементы.Однократно.Доступность = Объект.ГИГ_ОЕМПотребность;
	Элементы.ГИГ_Партнер.Доступность = Объект.ГИГ_ОЕМПотребность;
	Элементы.ГИГ_ПредставлениеОЕМПотребности.Доступность = Объект.ГИГ_ОЕМПотребность;
	Если Не Объект.ГИГ_ОЕМПотребность Тогда
		Объект.Рин1_ОЕМ_однократная = Ложь;
		Объект.ГИГ_Партнер = Справочники.Партнеры.ПустаяСсылка();
		Объект.ГИГ_ПредставлениеОЕМПотребности = "";
	КонецЕсли;
//}}20201005 ГлазуновДВ		
КонецПроцедуры

&НаСервере
Процедура ГИГ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка) //ОЕМПотребность
	
	ГИГ_ОЕМПотребностьУстановитьВидимостьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ГИГ_ПрограммноеДобавлениеЭлементовНаСервере()
	
	Элемент_Группа = Элементы.Добавить("ГруппаОЕМПотребность", Тип("ГруппаФормы"), Элементы.Шапка);
	Элемент_Группа.Заголовок = "ОЕМ потребность";
	Элемент_Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Элемент_Группа.Отображение = ОтображениеОбычнойГруппы.СильноеВыделение;
	Элемент_Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
//{{20201005 ГлазуновДВ добавили "ГруппаФлажков"
	Элемент_Группа1 = Элементы.Добавить("ГруппаФлажков", Тип("ГруппаФормы"), Элемент_Группа);
	//Элемент_Группа1.Заголовок = "";
	Элемент_Группа1.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Элемент_Группа1.ОтображатьЗаголовок = Ложь;
	//Элемент_Группа1.Отображение = ОтображениеОбычнойГруппы.Нет;
	Элемент_Группа1.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
//}}20201005 ГлазуновДВ
	
	Элемент = Элементы.Добавить("ГИГ_ОЕМПотребность", Тип("ПолеФормы"), Элемент_Группа1); 
//	Элемент = Элементы.Добавить("ГИГ_ОЕМПотребность", Тип("ПолеФормы"), Элемент_Группа); 
	Элемент.Вид = ВидПоляФормы.ПолеФлажка;
	Элемент.ПутьКДанным = "Объект.ГИГ_ОЕМПотребность"; 
	Элемент.Заголовок = "ОЕМ потребность";
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	Элемент.УстановитьДействие("ПриИзменении", "ГИГ_ГИГ_ОЕМПотребностьПриИзменении");

//{{20201005 ГлазуновДВ
	Элемент = Элементы.Добавить("Однократно", Тип("ПолеФормы"), Элемент_Группа1); 
	Элемент.Вид = ВидПоляФормы.ПолеФлажка;
	Элемент.ПутьКДанным = "Объект.Рин1_ОЕМ_однократная";
	Элемент.Заголовок = "Однократно";
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
//}}20201005 ГлазуновДВ
	
	Элемент = Элементы.Добавить("ГИГ_Партнер", Тип("ПолеФормы"), Элемент_Группа); 
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "Объект.ГИГ_Партнер"; 
	Элемент.Заголовок = "Партнер";
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	
	Элемент = Элементы.Добавить("ГИГ_ПредставлениеОЕМПотребности", Тип("ПолеФормы"), Элемент_Группа); 
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "Объект.ГИГ_ПредставлениеОЕМПотребности"; 
	Элемент.Заголовок = "Представление ОЕМ потребности";
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	
КонецПроцедуры

&НаСервере
Процедура Рин1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ГИГ_ПрограммноеДобавлениеЭлементовНаСервере();
	ГИГ_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
&Вместо("Подключаемый_ОбработатьЗаписьОбъекта")
Процедура Рин1_Подключаемый_ОбработатьЗаписьОбъекта()
	
	Если Объект.ГИГ_ОЕМПотребность Тогда
		Для каждого Строка Из Объект.Товары Цикл
			Если не Строка.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Обособленно") Тогда
				Сообщить("Документ с ОЕМ потребностью. Все строки табличной части товары должны обеспечиваться обособленно!");
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиент.ОбработатьЗаписьОбъектаВФорме(ЭтотОбъект, ПараметрыДляЗаписи);
	
КонецПроцедуры

// + [Rineco], [Киселев А.Н.] [10.09.2021] 
// Задача: [№ 1853], [#Контроль удаленных позций в ТЧ]
&НаКлиенте
Процедура Рин1_ПередЗаписьюПеред(Отказ, ПараметрыЗаписи)
	Ринэко_КонтрольУдаленныхПозицийКлиент.НачатьКонтрольУдаленныхПозиций(ЭтотОбъект,Отказ,ПараметрыЗаписи);
КонецПроцедуры
// - [Rineco], [Киселев А.Н.] [10.09.2021]
