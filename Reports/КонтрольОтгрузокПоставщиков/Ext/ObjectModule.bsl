
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	

	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	//Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;  
	// - Если сделать так, как показано выше(рекомендуют на некоторых ресурсах), то при изменении настроек в режиме клиента
	// этих изменений Вы не увидите, потому что настройки всегда будут по умолчанию. Как правильно - вариант ниже
	Настройки = КомпоновщикНастроек.ПолучитьНастройки(); 
 
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных; 
 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);	
	
	пТекущаяДата= ТекущаяДата();
	
	пПериодОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Период");
	
	тзТаблица = СформироватьТаблицу(пПериодОтчета.Значение.ДатаНачала,пПериодОтчета.Значение.ДатаОкончания,пТекущаяДата);
	
	ВнешнийНаборДанных = Новый Структура("ТаблицаИсполненияЗаказовПоставщика", тзТаблица); 
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНаборДанных, ДанныеРасшифровки); 
	
	ДокументРезультат.Очистить();
	
 	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент; 
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат); 
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

Функция СформироватьТаблицу(пДатаНачала,пДатаОкончания,пТекущаяДата) экспорт
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	МВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МВТ;

	Запрос.Текст = ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("ДатаНачала",пДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",пДатаОкончания);
	Запрос.УстановитьПараметр("ТекущаяДата",пТекущаяДата);
	
	Попытка
		Результат = Запрос.ВыполнитьПакет();
	Исключение
		     Возврат 0;
	КонецПопытки;
	
	ИтоговаяТаблица = Результат[Результат.ВГраница()-2].Выгрузить(); 
	ПоступленияПоОрдерам = Результат[Результат.ВГраница()-1].Выгрузить(); 
	ПоступленияБезОрдеров = Результат[Результат.ВГраница()].Выгрузить(); 

	Для Каждого СтрТз из ИтоговаяТаблица Цикл 
		Если ЗначениеЗаполнено(СтрТз.ДокументОтгрузки)  Тогда   				
			СтруктураПоиска = Новый Структура("ДокументОтгрузки, Назначение, Характеристика, Номенклатура", СтрТз.ДокументОтгрузки,СтрТз.Назначение,СтрТз.Характеристика,СтрТз.Номенклатура);
			Если СтрТз.Склад.ИспользоватьОрдернуюСхемуПриПоступлении Тогда
				СтрокиПриемка = ПоступленияПоОрдерам.НайтиСтроки(СтруктураПоиска);
			Иначе
				СтрокиПриемка = ПоступленияБезОрдеров.НайтиСтроки(СтруктураПоиска);
			КонецЕсли;
			
			пКоличествоСтрок = СтрокиПриемка.Количество();
			пИндекс = 0;
			Пока пИндекс < пКоличествоСтрок Цикл 
					Если СтрТз.КоличествоОтгруженоФакт <= СтрокиПриемка[пИндекс].КоличествоПринято Тогда
						СтрТз.КоличествоФакт = СтрТз.КоличествоОтгруженоФакт;
						СтрокиПриемка[пИндекс].КоличествоПринято = СтрокиПриемка[пИндекс].КоличествоПринято - СтрТз.КоличествоОтгруженоФакт;
						СтрТз.ДатаПоступленияФакт = СтрокиПриемка[пИндекс].ДатаПоступленияФакт;
					ИначеЕсли СтрокиПриемка[пИндекс].КоличествоПринято > 0 Тогда 
						НоваяСтр = ИтоговаяТаблица.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтр,СтрТз);
						СтрТз.КоличествоФакт = СтрокиПриемка[пИндекс].КоличествоПринято;
						СтрТз.ДатаПоступленияФакт = СтрокиПриемка[пИндекс].ДатаПоступленияФакт;
						СтрТз.КоличествоОтгруженоФакт = СтрокиПриемка[пИндекс].КоличествоПринято;
						
						НоваяСтр.КоличествоОтгруженоФакт = НоваяСтр.КоличествоОтгруженоФакт - СтрокиПриемка[пИндекс].КоличествоПринято;
						СтрокиПриемка[пИндекс].КоличествоПринято = 0;

					КонецЕсли;				
				пИндекс = пИндекс + 1;
			КонецЦикла;
        КонецЕсли;
	КонецЦикла;
	
	
	// + [Rineco], [Киселев А.Н.] [27.10.2021] 
	// Задача: [№ 21257], [#Контроль отгрузок поставщиков]
	
	ИтоговаяТаблица.Колонки.Добавить("ДнейПросрочки",ОбщегоНазначения.ОписаниеТипаЧисло(15,3));
	
	Для Каждого ЭлементТаблицы Из ИтоговаяТаблица Цикл 
		
		Если ЗначениеЗаполнено(ЭлементТаблицы.ДатаПоступленияПлан) И ЗначениеЗаполнено(ЭлементТаблицы.ДатаПоступленияФакт) И ЭлементТаблицы.ДатаПоступленияПлан < ЭлементТаблицы.ДатаПоступленияФакт  Тогда
			ЭлементТаблицы.ДнейПросрочки = (НачалоДня(ЭлементТаблицы.ДатаПоступленияФакт) - НачалоДня(ЭлементТаблицы.ДатаПоступленияПлан)) / (60 * 60 * 24);
		Иначе 
			ЭлементТаблицы.ДнейПросрочки = Дата(1,1,1) 
		КонецЕсли;
		
	КонецЦикла;
	
	// - [Rineco], [Киселев А.Н.] [27.10.2021]
	
	
	Возврат ИтоговаяТаблица;
	УстановитьПривилегированныйРежим(Ложь);
КонецФункции

Функция ПолучитьТекстЗапроса()
	Возврат "ВЫБРАТЬ
	        |	ЗаказыПоставщикам.Регистратор КАК Регистратор,
	        |	ЗаказыПоставщикам.ЗаказПоставщику КАК ЗаказПоставщику,
	        |	ЗаказыПоставщикам.Номенклатура КАК Номенклатура,
	        |	ЗаказыПоставщикам.Характеристика КАК Характеристика,
	        |	ЗаказыПоставщикам.КодСтроки КАК КодСтроки,
	        |	ЗаказыПоставщикам.Склад КАК Склад,
	        |	СУММА(ЗаказыПоставщикам.Заказано) КАК Количество,
	        |	ЗаказыПоставщикам.ВидДвижения КАК ВидДвижения
	        |ПОМЕСТИТЬ ДвиженияПоЗаказу
	        |ИЗ
	        |	РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
	        |ГДЕ
	        |	ЗаказыПоставщикам.ЗаказПоставщику.Дата >= &ДатаНачала
	        |	И ЗаказыПоставщикам.ЗаказПоставщику.Дата <= &ДатаОкончания
	        |	И (ЗаказыПоставщикам.ЗаказПоставщику.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Подтвержден)
	        |			ИЛИ ЗаказыПоставщикам.ЗаказПоставщику.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт))
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ЗаказыПоставщикам.ЗаказПоставщику,
	        |	ЗаказыПоставщикам.Номенклатура,
	        |	ЗаказыПоставщикам.Характеристика,
	        |	ЗаказыПоставщикам.КодСтроки,
	        |	ЗаказыПоставщикам.Склад,
	        |	ЗаказыПоставщикам.Регистратор,
	        |	ЗаказыПоставщикам.ВидДвижения
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ДвиженияПоЗаказу.ЗаказПоставщику КАК ЗаказПоставщику,
	        |	ДвиженияПоЗаказу.Номенклатура КАК Номенклатура,
	        |	ДвиженияПоЗаказу.Характеристика КАК Характеристика,
	        |	ДвиженияПоЗаказу.КодСтроки КАК КодСтроки,
	        |	ДвиженияПоЗаказу.Склад КАК Склад,
	        |	СУММА(ДвиженияПоЗаказу.Количество) КАК Количество,
	        |	ЗаказПоставщикуТовары.Рин1_ПлановаяДатаПоступления КАК ПлановаяДатаПоступления,
	        |	ЗаказПоставщикуТовары.Рин1_ПлановаяДатаОтгрузки КАК ПлановаяДатаОтгрузки,
	        |	ЗаказПоставщикуТовары.Назначение КАК Назначение
	        |ПОМЕСТИТЬ ВТ_ЗаказыПоставщику
	        |ИЗ
	        |	ДвиженияПоЗаказу КАК ДвиженияПоЗаказу
	        |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	        |		ПО ДвиженияПоЗаказу.ЗаказПоставщику = ЗаказПоставщикуТовары.Ссылка
	        |			И ДвиженияПоЗаказу.КодСтроки = ЗаказПоставщикуТовары.КодСтроки
	        |ГДЕ
	        |	ДвиженияПоЗаказу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ДвиженияПоЗаказу.ЗаказПоставщику,
	        |	ДвиженияПоЗаказу.Номенклатура,
	        |	ДвиженияПоЗаказу.Характеристика,
	        |	ДвиженияПоЗаказу.КодСтроки,
	        |	ДвиженияПоЗаказу.Склад,
	        |	ЗаказПоставщикуТовары.Рин1_ПлановаяДатаПоступления,
	        |	ЗаказПоставщикуТовары.Рин1_ПлановаяДатаОтгрузки,
	        |	ЗаказПоставщикуТовары.Назначение
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ДвиженияПоЗаказу.Регистратор КАК Регистратор,
	        |	ДвиженияПоЗаказу.ЗаказПоставщику КАК ЗаказПоставщику,
	        |	ДвиженияПоЗаказу.Номенклатура КАК Номенклатура,
	        |	ДвиженияПоЗаказу.Характеристика КАК Характеристика,
	        |	ДвиженияПоЗаказу.КодСтроки КАК КодСтроки,
	        |	ДвиженияПоЗаказу.Склад КАК Склад,
	        |	СУММА(ДвиженияПоЗаказу.Количество) КАК Количество,
	        |	ВЫБОР
	        |		КОГДА ТИПЗНАЧЕНИЯ(ДвиженияПоЗаказу.Регистратор) = ТИП(Документ.ПриобретениеТоваровУслуг)
	        |			ТОГДА ВЫБОР
	        |					КОГДА ДвиженияПоЗаказу.Регистратор.ДатаВходящегоДокумента = ДАТАВРЕМЯ(1, 1, 1)
	        |						ТОГДА ДвиженияПоЗаказу.Регистратор.Дата
	        |					ИНАЧЕ ДвиженияПоЗаказу.Регистратор.ДатаВходящегоДокумента
	        |				КОНЕЦ
	        |		ИНАЧЕ ДвиженияПоЗаказу.Регистратор.Дата
	        |	КОНЕЦ КАК ДатаОтгрузкиФакт
	        |ПОМЕСТИТЬ ВТ_ДокументыОтргузки
	        |ИЗ
	        |	ДвиженияПоЗаказу КАК ДвиженияПоЗаказу
	        |ГДЕ
	        |	ДвиженияПоЗаказу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ДвиженияПоЗаказу.Регистратор,
	        |	ДвиженияПоЗаказу.ЗаказПоставщику,
	        |	ДвиженияПоЗаказу.Номенклатура,
	        |	ДвиженияПоЗаказу.Характеристика,
	        |	ДвиженияПоЗаказу.КодСтроки,
	        |	ДвиженияПоЗаказу.Склад,
	        |	ВЫБОР
	        |		КОГДА ТИПЗНАЧЕНИЯ(ДвиженияПоЗаказу.Регистратор) = ТИП(Документ.ПриобретениеТоваровУслуг)
	        |			ТОГДА ВЫБОР
	        |					КОГДА ДвиженияПоЗаказу.Регистратор.ДатаВходящегоДокумента = ДАТАВРЕМЯ(1, 1, 1)
	        |						ТОГДА ДвиженияПоЗаказу.Регистратор.Дата
	        |					ИНАЧЕ ДвиженияПоЗаказу.Регистратор.ДатаВходящегоДокумента
	        |				КОНЕЦ
	        |		ИНАЧЕ ДвиженияПоЗаказу.Регистратор.Дата
	        |	КОНЕЦ
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ЗаказыПоставщику.ЗаказПоставщику КАК ЗаказПоставщику,
	        |	ВТ_ЗаказыПоставщику.Номенклатура КАК Номенклатура,
	        |	ВТ_ЗаказыПоставщику.Характеристика КАК Характеристика,
	        |	ВТ_ЗаказыПоставщику.КодСтроки КАК КодСтроки,
	        |	ВТ_ЗаказыПоставщику.Склад КАК Склад,
	        |	ВТ_ЗаказыПоставщику.Количество КАК Количество,
	        |	ВТ_ЗаказыПоставщику.ПлановаяДатаПоступления КАК ПлановаяДатаПоступления,
	        |	ВТ_ЗаказыПоставщику.ПлановаяДатаОтгрузки КАК ПлановаяДатаОтгрузки,
	        |	ЕСТЬNULL(ВТ_ДокументыОтргузки.Количество, 0) КАК КоличествоОтгруженоФакт,
	        |	ЕСТЬNULL(ВТ_ДокументыОтргузки.ДатаОтгрузкиФакт, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаОтгрузкиФакт,
	        |	ЕСТЬNULL(ВТ_ЗаказыПоставщику.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК Назначение,
	        |	ЕСТЬNULL(ВТ_ДокументыОтргузки.Регистратор, ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)) КАК ДокументОтгрузки
	        |ПОМЕСТИТЬ ВТ_ИтоговаяТаблица
	        |ИЗ
	        |	ВТ_ЗаказыПоставщику КАК ВТ_ЗаказыПоставщику
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументыОтргузки КАК ВТ_ДокументыОтргузки
	        |		ПО ВТ_ЗаказыПоставщику.ЗаказПоставщику = ВТ_ДокументыОтргузки.ЗаказПоставщику
	        |			И ВТ_ЗаказыПоставщику.КодСтроки = ВТ_ДокументыОтргузки.КодСтроки
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ИтоговаяТаблица.ДокументОтгрузки КАК ДокументОтгрузки,
	        |	ВТ_ИтоговаяТаблица.Назначение КАК Назначение,
	        |	ВТ_ИтоговаяТаблица.Номенклатура КАК Номенклатура,
	        |	ВТ_ИтоговаяТаблица.Характеристика КАК Характеристика,
	        |	ВТ_ИтоговаяТаблица.Склад КАК Склад
	        |ПОМЕСТИТЬ ВТ_ДокументыОтгрузкиСводно
	        |ИЗ
	        |	ВТ_ИтоговаяТаблица КАК ВТ_ИтоговаяТаблица
	        |ГДЕ
	        |	ВТ_ИтоговаяТаблица.ДокументОтгрузки <> ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.Пустаяссылка)
	        |	И ВТ_ИтоговаяТаблица.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ИтоговаяТаблица.ДокументОтгрузки,
	        |	ВТ_ИтоговаяТаблица.Назначение,
	        |	ВТ_ИтоговаяТаблица.Номенклатура,
	        |	ВТ_ИтоговаяТаблица.Характеристика,
	        |	ВТ_ИтоговаяТаблица.Склад
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ИтоговаяТаблица.ЗаказПоставщику КАК ЗаказПоставщику,
	        |	ВТ_ИтоговаяТаблица.ЗаказПоставщику.ДокументОснование КАК ЗаказПоставщикуДокументОснование,
	        |	ВТ_ИтоговаяТаблица.Номенклатура КАК Номенклатура,
	        |	ВТ_ИтоговаяТаблица.Характеристика КАК Характеристика,
	        |	ВТ_ИтоговаяТаблица.Назначение КАК Назначение,
	        |	ВТ_ИтоговаяТаблица.ДокументОтгрузки КАК ДокументОтгрузки
	        |ПОМЕСТИТЬ ВТ_РаспоряженияБезОрдеров
	        |ИЗ
	        |	ВТ_ИтоговаяТаблица КАК ВТ_ИтоговаяТаблица
	        |ГДЕ
	        |	НЕ ВТ_ИтоговаяТаблица.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
	        |	И ВТ_ИтоговаяТаблица.ЗаказПоставщику.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	        |	И ВТ_ИтоговаяТаблица.ДокументОтгрузки <> ЗНАЧЕНИЕ(документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ИтоговаяТаблица.ЗаказПоставщику,
	        |	ВТ_ИтоговаяТаблица.ЗаказПоставщику.ДокументОснование,
	        |	ВТ_ИтоговаяТаблица.Номенклатура,
	        |	ВТ_ИтоговаяТаблица.Характеристика,
	        |	ВТ_ИтоговаяТаблица.Назначение,
	        |	ВТ_ИтоговаяТаблица.ДокументОтгрузки
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ИтоговаяТаблица.ЗаказПоставщику КАК ЗаказПоставщику,
	        |	ВТ_ИтоговаяТаблица.Номенклатура КАК Номенклатура,
	        |	ВТ_ИтоговаяТаблица.Характеристика КАК Характеристика,
	        |	ВТ_ИтоговаяТаблица.КодСтроки КАК КодСтроки,
	        |	ВТ_ИтоговаяТаблица.Склад КАК Склад,
	        |	СУММА(ВТ_ИтоговаяТаблица.Количество) КАК Количество,
	        |	ВТ_ИтоговаяТаблица.КоличествоОтгруженоФакт КАК КоличествоОтгруженоФакт,
	        |	ВТ_ИтоговаяТаблица.ПлановаяДатаПоступления КАК ДатаПоступленияПлан,
	        |	ВТ_ИтоговаяТаблица.ПлановаяДатаОтгрузки КАК ДатаОтгрузкиПлан,
	        |	ВТ_ИтоговаяТаблица.ДатаОтгрузкиФакт КАК ДатаОтгрузкиФакт,
	        |	ВТ_ИтоговаяТаблица.Назначение КАК Назначение,
	        |	МИНИМУМ(ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоступленияФакт,
	        |	СУММА(0) КАК КоличествоФакт,
	        |	ВТ_ИтоговаяТаблица.ЗаказПоставщику.Менеджер КАК Менеджер,
	        |	ВТ_ИтоговаяТаблица.ЗаказПоставщику.Партнер КАК Поставщик,
	        |	ВТ_ИтоговаяТаблица.ДокументОтгрузки КАК ДокументОтгрузки
	        |ИЗ
	        |	ВТ_ИтоговаяТаблица КАК ВТ_ИтоговаяТаблица
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ИтоговаяТаблица.ЗаказПоставщику,
	        |	ВТ_ИтоговаяТаблица.Номенклатура,
	        |	ВТ_ИтоговаяТаблица.Характеристика,
	        |	ВТ_ИтоговаяТаблица.КодСтроки,
	        |	ВТ_ИтоговаяТаблица.Склад,
	        |	ВТ_ИтоговаяТаблица.КоличествоОтгруженоФакт,
	        |	ВТ_ИтоговаяТаблица.ПлановаяДатаПоступления,
	        |	ВТ_ИтоговаяТаблица.ПлановаяДатаОтгрузки,
	        |	ВТ_ИтоговаяТаблица.ДатаОтгрузкиФакт,
	        |	ВТ_ИтоговаяТаблица.Назначение,
	        |	ВТ_ИтоговаяТаблица.ЗаказПоставщику.Менеджер,
	        |	ВТ_ИтоговаяТаблица.ЗаказПоставщику.Партнер,
	        |	ВТ_ИтоговаяТаблица.ДокументОтгрузки
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	ЗаказПоставщику,
	        |	КодСтроки,
	        |	ДатаПоступленияПлан
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ДокументыОтгрузкиСводно.ДокументОтгрузки КАК ДокументОтгрузки,
	        |	ВТ_ДокументыОтгрузкиСводно.Назначение КАК Назначение,
	        |	ВТ_ДокументыОтгрузкиСводно.Номенклатура КАК Номенклатура,
	        |	ВТ_ДокументыОтгрузкиСводно.Характеристика КАК Характеристика,
	        |	ВТ_ДокументыОтгрузкиСводно.Склад КАК Склад,
	        |	СУММА(ЕСТЬNULL(ТоварыКПоступлениюОбороты.КОформлениюПоступленийПоОрдерамПриход, 0)) КАК КоличествоПринято,
	        |	ТоварыКПоступлениюОбороты.Регистратор.Дата КАК ДатаПоступленияФакт
	        |ИЗ
	        |	ВТ_ДокументыОтгрузкиСводно КАК ВТ_ДокументыОтгрузкиСводно
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКПоступлению.Обороты(&ДатаНачала, &ТекущаяДата, Регистратор, ) КАК ТоварыКПоступлениюОбороты
	        |		ПО ВТ_ДокументыОтгрузкиСводно.Номенклатура = ТоварыКПоступлениюОбороты.Номенклатура
	        |			И ВТ_ДокументыОтгрузкиСводно.Назначение = ТоварыКПоступлениюОбороты.Назначение
	        |			И ВТ_ДокументыОтгрузкиСводно.Характеристика = ТоварыКПоступлениюОбороты.Характеристика
	        |			И ВТ_ДокументыОтгрузкиСводно.Склад = ТоварыКПоступлениюОбороты.Склад
	        |			И ВТ_ДокументыОтгрузкиСводно.ДокументОтгрузки = ТоварыКПоступлениюОбороты.ДокументПоступления
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ДокументыОтгрузкиСводно.ДокументОтгрузки,
	        |	ВТ_ДокументыОтгрузкиСводно.Назначение,
	        |	ВТ_ДокументыОтгрузкиСводно.Номенклатура,
	        |	ВТ_ДокументыОтгрузкиСводно.Характеристика,
	        |	ВТ_ДокументыОтгрузкиСводно.Склад,
	        |	ТоварыКПоступлениюОбороты.Регистратор.Дата
	        |
	        |ИМЕЮЩИЕ
	        |	СУММА(ЕСТЬNULL(ТоварыКПоступлениюОбороты.КОформлениюПоступленийПоОрдерамПриход, 0)) > 0
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	ДатаПоступленияФакт
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_РаспоряженияБезОрдеров.ЗаказПоставщику КАК ЗаказПоставщику,
	        |	ВТ_РаспоряженияБезОрдеров.Номенклатура КАК Номенклатура,
	        |	ВТ_РаспоряженияБезОрдеров.Характеристика КАК Характеристика,
	        |	ВТ_РаспоряженияБезОрдеров.Назначение КАК Назначение,
	        |	ВТ_РаспоряженияБезОрдеров.ДокументОтгрузки КАК ДокументОтгрузки,
	        |	СУММА(ЕСТЬNULL(ТоварыКОтгрузкеОбороты.КОтгрузкеРасход, 0)) КАК КоличествоПринято,
	        |	ТоварыКОтгрузкеОбороты.Регистратор.Дата КАК ДатаПоступленияФакт,
	        |	ВТ_РаспоряженияБезОрдеров.ЗаказПоставщикуДокументОснование КАК ЗаказПоставщикуДокументОснование
	        |ИЗ
	        |	ВТ_РаспоряженияБезОрдеров КАК ВТ_РаспоряженияБезОрдеров
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОтгрузке.Обороты(&ДатаНачала, &ТекущаяДата, Регистратор, ) КАК ТоварыКОтгрузкеОбороты
	        |		ПО ВТ_РаспоряженияБезОрдеров.ЗаказПоставщикуДокументОснование = ТоварыКОтгрузкеОбороты.ДокументОтгрузки
	        |			И ВТ_РаспоряженияБезОрдеров.Номенклатура = ТоварыКОтгрузкеОбороты.Номенклатура
	        |			И ВТ_РаспоряженияБезОрдеров.Характеристика = ТоварыКОтгрузкеОбороты.Характеристика
	        |			И ВТ_РаспоряженияБезОрдеров.Назначение = ТоварыКОтгрузкеОбороты.Назначение
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_РаспоряженияБезОрдеров.ЗаказПоставщику,
	        |	ВТ_РаспоряженияБезОрдеров.Номенклатура,
	        |	ВТ_РаспоряженияБезОрдеров.Характеристика,
	        |	ВТ_РаспоряженияБезОрдеров.Назначение,
	        |	ВТ_РаспоряженияБезОрдеров.ДокументОтгрузки,
	        |	ТоварыКОтгрузкеОбороты.Регистратор.Дата,
	        |	ВТ_РаспоряженияБезОрдеров.ЗаказПоставщикуДокументОснование
	        |
	        |ИМЕЮЩИЕ
	        |	СУММА(ЕСТЬNULL(ТоварыКОтгрузкеОбороты.КОтгрузкеРасход, 0)) > 0
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	ДатаПоступленияФакт";
КонецФункции
