
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ИспользоватьХарактеристики = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	
	Если ИспользоватьХарактеристики Тогда
		Если Не Справочники.Номенклатура.ХарактеристикиИспользуются(Номенклатура) Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Характеристика");
		КонецЕсли;
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("Характеристика");
	КонецЕсли;
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ, ПараметрыПроверки);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Цена = Товары.Итог("Сумма") + Трудозатраты.Итог("Сумма");
	Для Каждого ТекСтрока Из ЭтапыПроизводства Цикл
		Если ЗначениеЗаполнено(ТекСтрока.ДлительностьЭтапа) И Не ЗначениеЗаполнено(ТекСтрока.ЕдиницаИзмеренияДлительностиЭтапа) Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнена единица измерения длительности этапа в строке №"+ТекСтрока.НомерСтроки;
			Сообщение.Поле = "ЭтапыПроизводства["+(ТекСтрока.НомерСтроки-1)+"].ЕдиницаИзмеренияДлительностиЭтапа";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Наименование = "";
	Ответственный = Справочники.Пользователи.ПустаяСсылка();
	Комментарий = "";
	
КонецПроцедуры
